' Resources:
' arcade.jpg - Teddy bear, Arcade, Claw machines image. Free for use. (https://pixabay.com/photos/teddy-bear-arcade-claw-machines-7558723/)
' bionic-bunny.jpg - Bionic Bunny Soldier by Zara
' bionic-bunny-commander.jpg - Bionic Bunny Commander by Zara
' bonfire.mp3 - bonfire flames sizzling (https://freesound.org/people/florianreichelt/sounds/563764/)
' book.png - Empty book by Darkmoon_Art (https://pixabay.com/illustrations/reserve-pages-empty-book-open-book-3057904/)
' boom.png - Explosion Detonation Blast royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/explosion-detonation-blast-burst-147909/)
' boss-battle.mp3 - Neon Dreams: A Retro-Futuristic Synthwave Track - MP3 Mixdown (https://freesound.org/people/Robbnix/sounds/685538/)
' bow-and-arrow.png and arrow.png - Arrow Bow Weapon royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/arrow-bow-weapon-medieval-weapon-161517/)
' bunny.jpg -  Rabbit, Field, Animal image. Free for use. by SvenZiegler (https://pixabay.com/photos/rabbit-field-animal-field-hare-6063733/)
' bunny2.jpg - Hare, Animal, Mammal image. Free for use. by apnear40 (https://pixabay.com/photos/hare-animal-mammal-wildlife-fauna-8585802/)
' castle.mp3 - Royal vibes (harp guitar and tuba) (https://freesound.org/people/kbrecordzz/sounds/595865/)
' castle-interior.jpg - Ai Generated Gothic Castle royalty-free stock illustration. Free for use & download. (https://pixabay.com/illustrations/ai-generated-gothic-castle-room-8469399/)
' cat-meow.wav - my hungry cat meowing (https://freesound.org/people/tuberatanka/sounds/110010/)
' cat-meowing.mp3 - meow of a cat (https://freesound.org/people/lextrack/sounds/333916/)
' cat-missile-left.png and cat-missile-right.png - Cat Kitten Animal royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/cat-kitten-lazy-tired-sleepy-pet-8633535/)
' clouds.png - OpenClipart-Vectors (https://pixabay.com/vectors/sunrise-clouds-rising-sun-sky-sun-153600/)
' death.mp3 - Dramatic Scene Character Death Separation Melancholy Sadness (https://freesound.org/people/UNIVERSFIELD/sounds/697083/)
' dragon.mp3 - The Roar of a 5-Headed Dragon (https://freesound.org/people/bevibeldesign/sounds/366095/)
' dragon.png - Animal Beast Creature royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/animal-beast-creature-dragon-drake-2027043/)
' dragon-fight-grey.png - Dragon Fantasy Samurai royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/dragon-fantasy-samurai-forest-7063555/)
' dragon-fight-grey2.png - (Expanded) Dragon Fantasy Samurai royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/dragon-fantasy-samurai-forest-7063555/)
' dragon-fight-zara.png - Dragon Katana Woman royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/dragon-katana-woman-fantasy-sword-7063556/)
' dragon-fight-zara2.png - (Expanded) Dragon Katana Woman royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/dragon-katana-woman-fantasy-sword-7063556/)
' dragon-fire.jpg - Fire Breathing Fiction royalty-free stock illustration. Free for use & download. (https://pixabay.com/illustrations/fire-breathing-fiction-dragon-7853170/)
' dragon-water.gif - Dragon, Fire, Reptile GIF. Free for use. (https://pixabay.com/gifs/dragon-fire-reptile-water-sea-sky-3473/)
' elevator.wav - music elevator ext (https://freesound.org/people/Jay_You/sounds/467240/)
' elevator-doors.png - Krolestwo_Nauki (https://pixabay.com/vectors/elevator-elevator-door-building-6232385/)
' elevator-open.mp3 - Elevator ding door open close, noisy, 10.mp3 (https://freesound.org/people/TRP/sounds/576359/)
' epic.mp3 - Epic Trailer Background Music (https://freesound.org/people/Migfus20/sounds/560454/)
' explode.png - Explosion Explode Nature royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/explosion-explode-colorful-abstract-2750400/)
' fireball.mp3 - 8 Bit Fireball (https://freesound.org/people/Jofae/sounds/368511/)
' fireball1.png - Fire Hot Flame royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/fire-hot-flame-power-heat-danger-305341/)
' fireball2.png - Fire Flame Danger royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/fire-flame-danger-burn-light-306894/)
' fireball3.png - Fire Hot Flame royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/fire-hot-flame-power-heat-danger-305340/)
' fireball4.png - Fire Flame Red royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/fire-flame-red-heat-hot-305227/)
' fluff.jpg - Gallant Sir Fluff on a mountain by Mr. Chris
' fluff2.jpg - Gallant Sir Fluff looking perplexed by Mr. Chris
' fluff.wav - Spanish Lick I, Acoustic- AB split stereo pair (Oktava).wav (https://freesound.org/people/debudding/sounds/44366/)
' game-over.mp3 - Game Over - low (https://freesound.org/people/MT4000/sounds/664606/)
' gianni.jpg - Son of Fluff climbing a stair railing by Mr. Chris
' gianni2.jpg - Son of Fluff curled up on a blanket by Mr. Chris
' gold.png - Chest Coins Gold royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/chest-coins-gold-lock-money-1293748/)
' grey.mp3 - 8-Bit Coin Or Power Up FX For Retro Video Games (https://freesound.org/people/Cloud-10/sounds/647977/)
' heavy-wings.mp3 - Wing Flap Heavy (prototype).mp3 (https://freesound.org/people/GearArcanaNo37/sounds/561364/)
' hit.mp3 - Hit with frying pan.mp3 (https://freesound.org/people/Whiprealgood/sounds/529567/)
' hit-harder.mp3 - frying pan.mp3 (https://freesound.org/people/Taira%20Komori/sounds/215017/)
' hooting.mp3 - Desert Owl (https://freesound.org/people/claytron3000/sounds/384186/)
' inventory.mp3 - Cash Register (https://freesound.org/people/kiddpark/sounds/201159/)
' magic.mp3 - Magic.mp3 (https://freesound.org/people/Bastianhallo/sounds/434050/)
' magic-humming.mp3 - Magical Humming Ambiance. (https://freesound.org/people/SnowFightStudios/sounds/670641/)
' magic-chimes.mp3 - a mystical enclosed bell (https://freesound.org/people/kdemani/sounds/635328/)
' minigames.mp3 - video game menu music (https://freesound.org/people/magmadiverrr/sounds/661248/)
' mushroom.jpg - Crystal, Mushroom, Purple mushroom image. Free for use. by Fractals99 (https://pixabay.com/photos/crystal-mushroom-purple-mushroom-7428278/)
' mylee.jpg - Mylee Marie by Mr. Chris
' mylee-sink.jpg - Mylee in a sink by Mr. Chris
' night-owl.jpg - Scary, Owl, Eyes image. Free for use. by Skitterphoto (https://pixabay.com/photos/scary-owl-eyes-spooky-halloween-3595742/)
' night-owl-flying.png - Owl Bird Nature royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/owl-bird-geometric-animal-wings-5986451/)
' ninja-cat.png - Ai Generated Cat Ninja royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/ai-generated-cat-ninja-stealth-8471008/)
' old-books.jpg - Old books, Book, Old image. Free for use. by jarmoluk (https://pixabay.com/photos/old-books-book-old-library-436498/)
' points.mp3 - 320655__rhodesmas__level-up-01.mp3 (https://freesound.org/people/shinephoenixstormcrow/sounds/337049/)
' pause.mp3 - This is a sound I created to the game https://apps.facebook.com/social-snakes/ using Garage Band.  It's a small sound, just like old school video games.  The "pause" sound it's simply the notes C, D, E.  (https://freesound.org/people/crisstanza/sounds/167127/)
' scream.mp3 - Human Death Scream.mp3 (https://freesound.org/people/DjCHAOS/sounds/77148/)
' select-player.png - Image by johngelling from Pixabay (https://pixabay.com/vectors/people-man-woman-child-boy-girl-1886412/)
' select-player-grey.png - Image by johngelling from Pixabay (https://pixabay.com/vectors/people-man-woman-child-boy-girl-1886412/)
' select-player-zara.png - Image by johngelling from Pixabay (https://pixabay.com/vectors/people-man-woman-child-boy-girl-1886412/)
' store.jpg - Shoe repair, Shop, Store image. Free for use. (https://pixabay.com/photos/shoe-repair-shop-store-guerrande-2498429/)
' suspense.mp3 - Drone Background Music (https://freesound.org/people/Migfus20/sounds/561085/)
' turn-page.mp3 - page turn (https://freesound.org/people/partheeban/sounds/457767/)
' uno-reverse.png - Uno reverse card Blank Template - Imgflip (https://imgflip.com/memetemplate/371071451/Uno-reverse-card)
' victory.mp3 - Success Fanfare Trumpets.mp3 - A simple success/triumph/resolution fanfare made using the brass sound on Musescore. Enjoy! (https://freesound.org/people/FunWithSound/sounds/456966/)
' wayne-chung.jpg - Image of Wayne Chung wondering what's going on
' wayne-chung.mp3 - Wang Chung "Everybody Have Fun Tonight" snippet
' wilderness.jpg and wilderness-full.jpg - Nature, Fog, Travel image. Free for use. by jameswheeler (https://pixabay.com/photos/nature-fog-travel-foggy-haze-mist-3787200/)
' wilderness2.jpg - Flow, Forest, Landscape image. Free for use. by jggrz (https://pixabay.com/photos/flow-forest-landscape-nature-4689820/)
' wilderness3.jpg and wilderness3-small.jpg - Ai Generated Meadow Mountain royalty-free stock illustration. Free for use & download. (https://pixabay.com/illustrations/ai-generated-meadow-mountain-8190587/)
' wilderness.mp3 - birds_210513_0088.mp3 (https://freesound.org/people/titi2/sounds/571247/)
' wind.mp3 - A 1 min. 25 sec. raw audio mp3 recording of a rather strong-sounding wind blowing through pine trees. The wind eventually gets a little lighter toward the end of the recording. Includes a bit of breeze on the microphone but only enough to add to the effect. Recorded with a black Sony IC digital voice recorder. (https://freesound.org/people/FunWithSound/sounds/390740/)
' wooden-sign.png - Sign Wood Wooden royalty-free vector graphic. Free for use & download. (https://pixabay.com/vectors/sign-wood-wooden-brown-rustic-576261/)
' woods.jpg - Secret, Forest, Darkness image. Free for use. by jplenio (https://pixabay.com/photos/secret-forest-darkness-nature-3120483/)
' woods2.jpg - Forest, Darkness, Trees image. Free for use. by Tomegos (https://pixabay.com/photos/forest-darkness-trees-dark-nature-7543646/)
' woods.mp3 - forestsurroundings.mp3 (https://freesound.org/people/Luftrum/sounds/48411/)
' zap.mp3 - Tesla coil sound. Synthesized. (https://freesound.org/people/YvesSch/sounds/143565/)
' zara.mp3 - 050816 a crowded grocery store Sapporo.mp3 (https://freesound.org/people/pflanigan/sounds/390254/)

' Declare that arrays are dynamic
Rem $DYNAMIC

' Define collection events as shared, constant values
Const CREATE = 1, RETRIEVE = 2, UPDATE = 3, DELETE = 4

' Define player names as shared, constant values
Const GREYSON = "GREYSON", GREY = "GREY", ZARA = "ZARA"

' Define game difficulty levels as shared, constant values
Const EASY = "EASY", HARD = "HARD"

' GIF stuff (https://qb64.com/wiki/GIF-Images)
Dim Shared Dbg: Dbg = 0
Dim Shared powerOf2&(11)
For a = 0 To 11: powerOf2&(a) = 2 ^ a: Next a

Type Variable
    id As Integer
    name As String
    type As String
    value As String
End Type

Type TextCursor
    lastColumn As Integer
    lastControlRow As Integer
    lastStoryRow As Integer
End Type

Type Scene
    id As Integer
    color As Long
    name As String
    sound As String
    symbol As String
End Type

Type MapDirections
    north As Integer
    east As Integer
    south As Integer
    west As Integer
End Type

' TODO - Extract map properties into a new Map type
Type Book
    author As String
    basicStoryChoices As String
    chapter As String
    controlChoices As String
    controlChoicesColumn As Integer
    controlChoicesRow As Integer
    credits As String
    cursor As TextCursor
    deathHitSound As Long
    difficulty As String
    directions As MapDirections
    doorX As Integer
    doorY As Integer
    experiencePoints As Integer
    firstRow As Integer
    heavyHitSound As Long
    highScores As String
    hitPoints As Integer
    hitSound As Long
    image As Long
    inventory As String
    inventorySound As Long
    isOver As Integer
    isRightHanded As Integer
    lastImageX As Integer
    lastImageY As Integer
    lastLevel As Integer
    lastRow As Integer
    leftPageStartingColumn As Integer
    level As Integer
    levelSize As Integer
    magicPoints As Integer
    maxPageLength As Integer
    openingGIF As String
    openingMusic As String
    pageNumber As Integer
    pageTurnSound As Long
    player As String
    playerDirection As Integer
    playerLastX As Integer
    playerLastY As Integer
    playerMode As String
    playerX As Integer
    playerY As Integer
    pointsSound As Long
    rightPageStartingColumn As Integer
    score As Integer
    secretChoices As String
    story As String
    storyChoices As String
    storyChoicesColumn As Integer
    storyChoicesRow As Integer
    storyFile As String
    storySound As Long
    title As String
    twin As String
    version As String
    victorySound As Long
End Type

Type Sprite
    action1 As String
    action2 As String
    action3 As String
    direction As Integer
    image As Long
    name As String
    size As Double
    speed As Integer
    visible As Integer
    x As Integer
    y As Integer
End Type

' GIF type (https://qb64.com/wiki/GIF-Images)
Type GIFDATA
    file As Integer
    sigver As String * 6
    width As _Unsigned Integer
    height As _Unsigned Integer
    bpp As _Unsigned _Byte
    sortFlag As _Byte ' Unused
    colorRes As _Unsigned _Byte
    colorTableFlag As _Byte
    bgColor As _Unsigned _Byte
    aspect As Single ' Unused
    numColors As _Unsigned Integer
    palette As String * 768
End Type

' Another GIF type (https://qb64.com/wiki/GIF-Images)
Type FRAMEDATA
    addr As Long
    left As _Unsigned Integer
    top As _Unsigned Integer
    width As _Unsigned Integer
    height As _Unsigned Integer
    localColorTableFlag As _Byte
    interlacedFlag As _Byte
    sortFlag As _Byte ' Unused
    palBPP As _Unsigned _Byte
    minimumCodeSize As _Unsigned _Byte
    transparentFlag As _Byte 'GIF89a-specific (animation) values
    userInput As _Byte ' Unused
    disposalMethod As _Unsigned _Byte
    delay As Single
    transColor As _Unsigned _Byte
End Type

' Initialize the game
Dim game As Book
game.directions.north = 1
game.directions.east = 2
game.directions.south = 3
game.directions.west = 4
game.version = "0.1.1"
game.chapter = ""
game.isOver = 0
game.isRightHanded = 1 ' Are the game controls on the right page? (1/0)
game.image = _LoadImage("book.png", 32)
game.firstRow = 3 ' book.png allows for row 3 as the first row for text
game.lastRow = 46 ' book.png allows for row 46 as the last row for text
game.leftPageStartingColumn = 15 ' book.png allows for column 15 as the first column for starting text on the left page
game.rightPageStartingColumn = 83 ' book.png allows for column 83 as the first column for starting text on the right page
game.maxPageLength = 66 ' book.png allows for 66 characters of text on a single line of a single page
game.controlChoicesColumn = 0
game.controlChoicesRow = 0
game.storyChoices = "" ' current choices for the player
game.storyChoicesColumn = 0
game.storyChoicesRow = 0
game.player = "" ' GREYSON or ZARA
game.controlChoices = "CREDITS,FLIP BOOK,HIGH SCORES,EXIT GAME"
game.basicStoryChoices = "LOOK,LISTEN"
game.secretChoices = "MOVE AHEAD,MOVE BACKWARDS,TURN LEFT,TURN RIGHT,ALL THE THINGS"
game.score = 0
game.inventory = ""
game.storySound = 0
game.hitPoints = 100
game.magicPoints = 0
game.experiencePoints = 0

' Configure application window
_Title "Twin Quest " + game.version ' Set the application window's title
$Resize:On
'$Resize:Smooth
' TODO - $Resize:Smooth would be nice, but it cuts off the top of the book image and dorks with mouse coordinates tracking

' Display "splash screen"
Call DisplaySplashScreen(game)

' Initialize common sounds
game.hitSound = _SndOpen("hit.mp3")
game.heavyHitSound = _SndOpen("hit-harder.mp3")
game.deathHitSound = _SndOpen("fireball.mp3")
game.inventorySound = _SndOpen("inventory.mp3")
game.pointsSound = _SndOpen("points.mp3")
game.victorySound = _SndOpen("victory.mp3")

' Display pre-chapter pages with epic music
game.pageTurnSound = PlaySound&("turn-page.mp3", 1, 0)
game.chapter = "SELECT STORY"
Call LoadStory(game)
If game.openingGIF <> "" Then
    'Call DisplayGIFPage(game)
End If
game.storySound = PlaySound&(game.openingMusic, 1, 1)
game.chapter = ""
Call DisplayTitlePage(game)
game.chapter = "CREDITS"
Call DisplayCreditsPage(game)
game.chapter = "HIGH SCORES"
Call DisplayHighScoresPage(game)
game.chapter = "SELECT MAIN CHARACTER"
Call DisplaySelectPlayerPage(game)
game.chapter = "SELECT DIFFICULTY"
Call DisplaySelectDifficultyPage(game)

' Start the story
game.chapter = "INTRODUCTION"
game.pageNumber = 1
game.storyChoices = game.basicStoryChoices

' Main game loop
choice$ = ""
Do
    Call RefreshStoryPage(game, choice$)
    Call RefreshControlPage(game, "")

    If game.hitPoints = 0 Then
        game.isOver = 1
    End If

    If game.isOver Then
        Call PressAnyKey(game, 0)
        Call TurnPage(game, 0)
        _Continue
    End If

    Color _RGB(0, 0, 0)

    ' TODO - Refactor this to be a special story mode where you keep advancing pages in a scene
    If game.chapter = "INTRODUCTION" Then
        'Call DisplayIntroduction(game)

        Dim introduction As Scene
        Call WorldScene(RETRIEVE, 1, 0, 0, 0, introduction)
        Call WorldScenePage(RETRIEVE, introduction.id, game.pageNumber, pageContents$)
        Call DisplayStoryPage(game, introduction, pageContents$, "", 1)

        If game.chapter = "CHAPTER 1" Then
            ' Every first step in every great adventure is... a first step
            game.lastLevel = 0
            game.level = 1
            choice$ = "MOVE AHEAD"
        End If
    Else
        ' Prompt for choice
        game.pageNumber = 1
        Locate game.cursor.lastStoryRow + 1, 1
        choice$ = StoryPrompt$(game, "Story Choices", game.storyChoices, 1, 1, 40)
    End If
Loop While game.isOver = 0

Call StopSound(game.storySound)

If game.hitPoints = 0 Then
    Call PrintPageText(game, "story", CsrLin, "You have died.")
    Print
    game.storySound = PlaySound&("death.mp3", 1, 1)
End If

gameOverSound& = PlaySound&("game-over.mp3", 1, 0)

' Check if the player has earned a spot on the HIGH SCORES list
If game.score > 0 Then
    Call ChallengeHighScores(game)
End If

Locate game.cursor.lastStoryRow + 2, 1
Call PrintPageText(game, "story", CsrLin, " _____ _            _____           _")
Call PrintPageText(game, "story", CsrLin, "|_   _| |__   ___  | ____|_ __   __| |")
Call PrintPageText(game, "story", CsrLin, "  | | | '_ \ / _ \ |  _| | '_ \ / _` |")
Call PrintPageText(game, "story", CsrLin, "  | | | | | |  __/ | |___| | | | (_| |")
Call PrintPageText(game, "story", CsrLin, "  |_| |_| |_|\___| |_____|_| |_|\__,_|")
Call PrintPageText(game, "story", CsrLin + 1, "(Press any key to exit the program)")
Sleep

System ' Ends the program immediately and closes the application window

Sub AllTheThingsAction (game As Book)
    thingDone% = 0
    Dim variable As Variable

    If game.level = 1 Then
        variable.name = "isDragonDead"
        Call StoryVariable(RETRIEVE, variable)
        If variable.value = "0" Then
            variable.value = "1"
            Call StoryVariable(UPDATE, variable)
            Call IncreaseScore(game, 100)
            Call PrintPageText(game, "story", CsrLin, "You slew the dragon!")
            thingDone% = 1
        End If
        variable.name = "isDragonDead"
        Call StoryVariable(RETRIEVE, variable)
        If variable.value = "0" Then
            variable.value = "1"
            Call StoryVariable(UPDATE, variable)
            Call IncreaseScore(game, 100)
            Call PrintPageText(game, "story", CsrLin, "You tamed the elusive cat!")
            thingDone% = 1
        End If
        If Not Contains%(game.inventory, "RING OF TAMING", ",") Then
            Call IncreaseInventory(game, "RING OF TAMING")
            Call PrintPageText(game, "story", CsrLin, "You obtained the Ring of Taming!")
            Call PrintPageText(game, "story", CsrLin, "(Does not work on large creatures.)")
            thingDone% = 1
        End If
        If Not Contains%(game.inventory, "GOLD", ",") Then
            Call IncreaseInventory(game, "GOLD")
            Call IncreaseScore(game, 100)
            Call PrintPageText(game, "story", CsrLin, "You obtained the Gold!")
            thingDone% = 1
        End If
        If (game.player = "GREYSON") And (Not Contains%(game.inventory, "UNO REVERSE", ",")) Then
            Call IncreaseInventory(game, "UNO REVERSE")
            Call PrintPageText(game, "story", CsrLin, "You obtained the legendary UNO Reverse card!")
            thingDone% = 1
        End If
        If (game.player = "ZARA") And (Not Contains%(game.inventory, "SWORD", ",")) Then
            Call IncreaseInventory(game, "SWORD")
            Call PrintPageText(game, "story", CsrLin, "You obtained the legendary SWORD!")
            thingDone% = 1
        End If
        If thingDone% = 0 Then
            Call PrintPageText(game, "story", CsrLin, "You have done all the things for level " + LTrim$(Str$(game.level)) + ".")
        End If
    End If
End Sub

' TODO - Capture the graphical area for a given text region
'
' Parameters:
' game - Book instance
' startColumn - First column of text
' startRow - First row of text
' endColumn - Last column of text
' endRow - Last row of text
'
' Returns:
' Image handle
Function CaptureTextArea& (game As Book, startColumn As Integer, startRow As Integer, endColumn As Integer, endRow As Integer)
    ' Convert text columns and rows to graphical x-y coordinates
    x1% = (startColumn - 1) * _FontWidth
    x2% = endColumn * _FontWidth
    y1% = (startRow - 1) * _FontHeight
    y2% = endRow * _FontHeight
    CaptureTextArea& = _NewImage(x2% - x1%, y2% - y1%, 32) ' Init the text area
End Function

' Challenge the current set of high scores
'
' Parameters:
' game - Book instance
Sub ChallengeHighScores (game As Book)
    Dim highScores(1 To 10) As String
    For i% = 1 To 10
        highScores(i%) = ""
    Next i%
    rank% = 0
    If game.highScores <> "" Then
        highScoresCount% = ListLength%(game.highScores, "<br>")
        index% = 1
        For i% = 1 To highScoresCount%
            highScore$ = ListPiece$(game.highScores, "<br>", i%)
            score% = Val(ListPiece$(highScore$, "   ", 1))
            initials$ = ListPiece$(highScore$, "   ", 2)
            scoreDate$ = ListPiece$(highScore$, "   ", 3)
            If (rank% = 0) And (game.score > score%) Then
                rank% = index%
                index% = index% + 1 ' Move Over, Beethoven
            End If
            If index% <= 10 Then
                highScores(index%) = highScore$
            End If
            index% = index% + 1
        Next i%
    Else
        rank% = 1
    End If

    If rank% = 0 Then
        Exit Sub
    End If

    Print
    Call PrintPageText(game, "story", game.cursor.lastStoryRow + 2, "You have a new high score... rank " + LTrim$(Str$(rank%)) + "!")
    highScoreInitials$ = ""
    While Len(highScoreInitials$) <> 3
        ' TODO - Only alphanumeric characters should be allowed
        highScoreInitials$ = GetInput$(game, "Enter your initials", 0, 0, 3)
    Wend
    highScores(rank%) = LTrim$(Str$(game.score)) + "   " + highScoreInitials$ + "   " + Date$

    ' TODO - Read each file line and stop at the high scores
    Dim storyFile(1 To 1000) As String
    i% = 0
    Open game.storyFile For Input As #1
    Do Until EOF(1)
        Input #1, fileLine$
        If fileLine$ = "[high scores]" Then
            Exit Do
        End If
        i% = i% + 1
        storyFile(i%) = fileLine$
    Loop
    Close #1

    game.highScores = ""
    Open game.storyFile For Output As #1
    For j% = 1 To i%
        If InStr(1, storyFile(j%), ",") > 0 Then
            ' TODO - If the line contains a double quote, escape it
            Print #1, Chr$(34) + storyFile(j%) + Chr$(34) ' Restore the original file contents
        Else
            Print #1, storyFile(j%) ' Restore the original file contents
        End If
    Next j%
    Print #1, "[high scores]"
    For i% = 1 To 10
        If (highScores(i%) <> "") Then
            Print #1, highScores(i%)
            If game.highScores <> "" Then
                game.highScores = game.highScores + "<br>"
            End If
            game.highScores = game.highScores + highScores(i%)
        End If
    Next i%
    Close #1

    Call DisplayHighScoresPage(game)
End Sub

' Does a string contain a given token? (1/0)
'
' Parameters:
' myString - String to search
' token - Token to find
' delimiter - String delimiting the tokens, if any
'
' Returns:
' Token was found? (1/0)
Function Contains% (myString As String, token As String, delimiter As String)
    Contains% = InStr(1, (delimiter + myString + delimiter), (delimiter + token + delimiter)) > 0
End Function

' Decrease the player's hit points
'
' Parameters:
' game - Book instance
' points - Number of points by which to decrease the player's hit points
' updateDisplay - Update the display? (1/0)
Sub DecreaseHitPoints (game As Book, points As Integer, updateDisplay As Integer)
    game.hitPoints = game.hitPoints - points
    If game.hitPoints < 0 Then
        game.hitPoints = 0
        game.score = 0
    End If
    If points <= 10 Then
        Call ReplaySound(game.hitSound, 1, 0)
    Else
        If points <= 25 Then
            Call ReplaySound(game.heavyHitSound, 1, 0)
        Else
            Call ReplaySound(game.deathHitSound, 1, 0)
        End If
    End If

    If updateDisplay Then
        currentColumn% = Pos(0)
        currentRow% = CsrLin
        screenImage& = _CopyImage(0)
        redScreen& = _NewImage(_Width, _Height, 32)
        Screen redScreen&
        Cls , _RGB(255, 0, 0) ' Clear screen with red as the background color
        _Delay 0.25 ' Suspend program execution for a half second
        _PutImage , screenImage&, 0 ' Restore the application screen
        _PrintMode _KeepBackground ' Preserve the image background when printing text
        _FreeImage screenImage& ' Free up memory
        Locate currentRow%, currentColumn% ' Restore the current text row and column

        storyColor& = _DefaultColor
        Color _RGB(255, 140, 0)
        Call PrintPageText(game, "story", CsrLin + 1, "HP -" + LTrim$(Str$(points)))
        storyRow% = CsrLin
        Call RefreshControlPage(game, "")
        Color storyColor&
        Locate storyRow%, 1
    End If
End Sub

' Get files in current directory
'
' (Copied and modified from QB64 help to support Linux filesystems and not used Shared variables)
'
' Parameters:
' [spec] - File spec (ie, "*.txt").  Use empty string to return other files from last call.
' fileCount - Count of files in directory
'
' Returns:
' Last file discovered (decrements index each time this function is called)
Function DIR$ (spec As String, fileCount As Integer)
    Const TmpFile$ = "DIRINF0.TMP", ListMAX% = 500 'change maximum to suit your needs
    Static Ready%, Index%, DirList$()
    If Not Ready% Then ReDim DirList$(ListMAX%): Ready% = -1 'DIM array first use
    If spec$ > "" Then 'get file names when a spec is given
        If _OS$ = "WINDOWS" Then
            Shell _Hide "DIR " + spec$ + " /b > " + TmpFile$
        Else
            Shell _Hide "ls " + spec$ + " > " + TmpFile$
        End If
        Index% = 0: DirList$(Index%) = "": ff% = FreeFile
        Open TmpFile$ For Append As #ff%
        size& = LOF(ff%)
        Close #ff%
        If size& = 0 Then Kill TmpFile$: Exit Function
        Open TmpFile$ For Input As #ff%
        Do While Not EOF(ff%) And Index% < ListMAX%
            Index% = Index% + 1
            Line Input #ff%, DirList$(Index%)
        Loop
        fileCount = Index% 'SHARED variable can return the file count
        Close #ff%
        Kill TmpFile$
    Else
        If Index% > 0 Then
            Index% = Index% - 1 'no spec sends next file name
        End If
    End If
    DIR$ = DirList$(Index%)
End Function

' Display the credits page
'
' Parameters:
' game - Book instance
Sub DisplayCreditsPage (game As Book)
    Call TurnPage(game, 1)
    Dim scene As Scene
    scene.name = "CREDITS"
    scene.color = _RGB(0, 0, 0)
    Call DisplayStoryPage(game, scene, game.credits, "", 1)
    Call PressAnyKey(game, 0)
End Sub

' Display a GIF image (https://qb64.com/wiki/GIF-Images)
'
' Parameters:
' game - Book instance
' fileName - Name of GIF image file
Sub DisplayGIF (game As Book, fileName As String)
    ' Open gif file. This reads the headers and palette but not the image data.
    ' The array will be redimentioned to fit the exact number of frames in the file.

    Dim gifData As GIFDATA, frameData(0 To 0) As FRAMEDATA

    'fileName$ = "dragon.gif" '<<<<<<<<<<<< Enter a file name here!!!

    If Len(fileName) = 0 Then End
    openGif fileName, gifData, frameData()

    x% = (game.leftPageStartingColumn + 1) * _FontWidth
    y% = (game.firstRow + 2) * _FontHeight
    loadingMsgDisplayed% = 0
    loadingMessage$ = "Loading ..."

    ' Loop away.
    startSeconds% = Val(ListPiece$(Time$, ":", 3))
    frame = 0
    Do
        ' Request a frame. If it has been requested before, it is re-used,
        ' otherwise it is read and decoded from the file.
        frameImage& = getGifFrame&(gifData, frameData(), frame)
        _PutImage (x%, y%), frameImage&
        If (loadingMsgDisplayed% = 0) And (_Height(frameImage&) > -1) Then
            Locate game.firstRow + 2 + (_Ceil(_Height(frameImage&) / _FontHeight)) + 1, game.leftPageStartingColumn + (game.maxPageLength / 2) - (Len(loadingMessage$) / 2)
            Print loadingMessage$
            loadingMsgDisplayed% = 1
        End If
        _Delay frameData(frame).delay
        frame = (frame + 1) Mod (UBound(frameData) + 1)
        currentSeconds% = Val(ListPiece$(Time$, ":", 3))
        If startSeconds% > currentSeconds% Then
            timeElapsed% = (60 - startSeconds%) + currentSeconds%
        Else
            timeElapsed% = currentSeconds% - startSeconds%
        End If
    Loop Until timeElapsed% >= 5

    'Close the file and free the allocated frames.
    codeGif gifData, frameData()
End Sub

' TODO - GIF sub (https://qb64.com/wiki/GIF-Images)
'########################################################################################
Sub openGif (filename$, gifData As GIFDATA, frameData() As FRAMEDATA) Static
    file = FreeFile: gifData.file = file
    Open "B", gifData.file, filename$

    Get file, , gifData.sigver
    Get file, , gifData.width
    Get file, , gifData.height
    Get file, , byte~%%
    gifData.bpp = (byte~%% And 7) + 1
    gifData.sortFlag = (byte~%% And 8) > 0
    gifData.colorRes = (byte~%% \ 16 And 7) + 1
    gifData.colorTableFlag = (byte~%% And 128) > 0
    gifData.numColors = 2 ^ gifData.bpp
    Get file, , gifData.bgColor
    Get file, , byte~%%
    If byte~%% = 0 Then gifData.aspect = 0 Else gifData.aspect = (byte~%% + 15) / 64

    If gifData.sigver <> "GIF87a" And gifData.sigver <> "GIF89a" Then _Dest 0: Print "Invalid version": End
    If Not gifData.colorTableFlag Then _Dest 0: Print "No Color Table": End

    palette$ = Space$(3 * gifData.numColors)
    Get file, , palette$
    gifData.palette = palette$
    If Dbg And 1 Then
        Print "sigver         ="; gifData.sigver
        Print "width          ="; gifData.width
        Print "height         ="; gifData.height
        Print "bpp            ="; gifData.bpp
        Print "sortFlag       ="; gifData.sortFlag
        Print "colorRes       ="; gifData.colorRes
        Print "colorTableFlag ="; gifData.colorTableFlag
        Print "bgColor        ="; gifData.bgColor
        Print "aspect         ="; gifData.aspect
        Print "numColors      ="; gifData.numColors
        For i = 0 To gifData.numColors - 1
            Print Using "pal(###) = "; i;
            Print Hex$(_RGB32(Asc(gifData.palette, i * 3 + 1), Asc(gifData.palette, i * 3 + 2), Asc(gifData.palette, i * 3 + 3)))
        Next
    End If
    Do
        Get file, , byte~%%
        If Dbg And 2 Then Print "Chunk: "; Hex$(byte~%%)
        Select Case byte~%%
            Case &H2C ' Image Descriptor
                If frame > UBound(frameData) Then
                    ReDim _Preserve frameData(0 To frame * 2 - 1) As FRAMEDATA
                End If

                Get file, , frameData(frame).left
                Get file, , frameData(frame).top
                Get file, , frameData(frame).width
                Get file, , frameData(frame).height
                Get file, , byte~%%
                frameData(frame).localColorTableFlag = (byte~%% And 128) > 0
                frameData(frame).interlacedFlag = (byte~%% And 64) > 0
                frameData(frame).sortFlag = (byte~%% And 32) > 0
                frameData(frame).palBPP = (byte~%% And 7) + 1
                frameData(frame).addr = Loc(file) + 1

                If frameData(frame).localColorTableFlag Then
                    Seek file, Loc(file) + 3 * 2 ^ frameData(frame).palBPP + 1
                End If
                Get file, , frameData(frame).minimumCodeSize
                If Dbg And 2 Then
                    Print "addr                ="; Hex$(frameData(frame).addr - 1)
                    Print "left                ="; frameData(frame).left
                    Print "top                 ="; frameData(frame).top
                    Print "width               ="; frameData(frame).width
                    Print "height              ="; frameData(frame).height
                    Print "localColorTableFlag ="; frameData(frame).localColorTableFlag
                    Print "interlacedFlag      ="; frameData(frame).interlacedFlag
                    Print "sortFlag            ="; frameData(frame).sortFlag
                    Print "palBPP              ="; frameData(frame).palBPP
                    Print "minimumCodeSize     ="; frameData(frame).minimumCodeSize
                End If
                If localColors Then _Dest 0: Print "Local color table": End
                If frameData(frame).disposalMethod > 2 Then Print "Unsupported disposalMethod: "; frameData(frame).disposalMethod: End
                skipBlocks file

                frame = frame + 1
            Case &H3B ' Trailer
                Exit Do
            Case &H21 ' Extension Introducer
                Get file, , byte~%% ' Extension Label
                If Dbg And 2 Then Print "Extension Introducer: "; Hex$(byte~%%)
                Select Case byte~%%
                    Case &HFF, &HFE ' Application Extension, Comment Extension
                        skipBlocks file
                    Case &HF9
                        If frame > UBound(frameData) Then
                            ReDim _Preserve frameData(0 To frame * 2 - 1) As FRAMEDATA
                        End If

                        Get 1, , byte~%% ' Block Size (always 4)
                        Get 1, , byte~%%
                        frameData(frame).transparentFlag = (byte~%% And 1) > 0
                        frameData(frame).userInput = (byte~%% And 2) > 0
                        frameData(frame).disposalMethod = byte~%% \ 4 And 7
                        Get 1, , delay~%
                        If delay~% = 0 Then frameData(frame).delay = 0.1 Else frameData(frame).delay = delay~% / 100
                        Get 1, , frameData(frame).transColor
                        If Dbg And 2 Then
                            Print "frame           ="; frame
                            Print "transparentFlag ="; frameData(frame).transparentFlag
                            Print "userInput       ="; frameData(frame).userInput
                            Print "disposalMethod  ="; frameData(frame).disposalMethod
                            Print "delay           ="; frameData(frame).delay
                            Print "transColor      ="; frameData(frame).transColor
                        End If
                        skipBlocks file
                    Case Else
                        Print "Unsupported extension Label: "; Hex$(byte~%%): End
                End Select
            Case Else
                Print "Unsupported chunk: "; Hex$(byte~%%): End
        End Select
    Loop

    ReDim _Preserve frameData(0 To frame - 1) As FRAMEDATA
End Sub

' TODO - GIF sub (https://qb64.com/wiki/GIF-Images)
Sub skipBlocks (file)
    Do
        Get file, , byte~%% ' Block Size
        If Dbg And 2 Then Print "block size ="; byte~%%
        Seek file, Loc(file) + byte~%% + 1
    Loop While byte~%%
End Sub

' TODO - GIF function (https://qb64.com/wiki/GIF-Images)
Function getGifFrame& (gifData As GIFDATA, frameData() As FRAMEDATA, frame)
    If frameData(frame).addr > 0 Then
        If Dbg And 4 Then
            Print "addr                ="; Hex$(frameData(frame).addr - 1)
            Print "left                ="; frameData(frame).left
            Print "top                 ="; frameData(frame).top
            Print "width               ="; frameData(frame).width
            Print "height              ="; frameData(frame).height
            Print "localColorTableFlag ="; frameData(frame).localColorTableFlag
            Print "interlacedFlag      ="; frameData(frame).interlacedFlag
            Print "sortFlag            ="; frameData(frame).sortFlag
            Print "palBPP              ="; frameData(frame).palBPP
            Print "minimumCodeSize     ="; frameData(frame).minimumCodeSize
            Print "transparentFlag     ="; frameData(frame).transparentFlag
            Print "userInput           ="; frameData(frame).userInput
            Print "disposalMethod      ="; frameData(frame).disposalMethod
            Print "delay               ="; frameData(frame).delay
            Print "transColor          ="; frameData(frame).transColor
        End If
        w = frameData(frame).width
        h = frameData(frame).height
        img& = _NewImage(w, h, 256)
        frame& = _NewImage(gifData.width, gifData.height, 256)

        _Dest img&
        decodeFrame gifData, frameData(frame)

        _Dest frame&
        If frameData(frame).localColorTableFlag Then
            _CopyPalette img&
        Else
            For i = 0 To gifData.numColors - 1
                _PaletteColor i, _RGB32(Asc(gifData.palette, i * 3 + 1), Asc(gifData.palette, i * 3 + 2), Asc(gifData.palette, i * 3 + 3))
            Next
        End If

        If frame Then
            Select Case frameData(frame - 1).disposalMethod
                Case 0, 1
                    _PutImage , frameData(frame - 1).addr
                Case 2
                    Cls , gifData.bgColor
                    _ClearColor gifData.bgColor
            End Select
        Else
            Cls , gifData.bgColor
        End If

        If frameData(frame).transparentFlag Then
            _ClearColor frameData(frame).transColor, img&
        End If
        _PutImage (frameData(frame).left, frameData(frame).top), img&
        _FreeImage img&

        frameData(frame).addr = frame&
        _Dest 0
    End If

    getGifFrame& = frameData(frame).addr
End Function

' TODO - GIF sub (https://qb64.com/wiki/GIF-Images)
'############################################################################################
Sub decodeFrame (gifdata As GIFDATA, framedata As FRAMEDATA)
    Dim byte As _Unsigned _Byte
    Dim prefix(4095), suffix(4095), colorStack(4095)

    startCodeSize = gifdata.bpp + 1
    clearCode = 2 ^ gifdata.bpp
    endCode = clearCode + 1
    minCode = endCode + 1
    startMaxCode = clearCode * 2 - 1
    nvc = minCode
    codeSize = startCodeSize
    maxCode = startMaxCode

    If framedata.interlacedFlag Then interlacedPass = 0: interlacedStep = 8
    bitPointer = 0
    blockSize = 0
    blockPointer = 0
    x = 0
    y = 0

    file = gifdata.file
    Seek file, framedata.addr

    If framedata.localColorTableFlag Then
        palette$ = Space$(3 * 2 ^ framedata.palBPP)
        Get 1, , palette$

        For i = 0 To gifdata.numColors - 1
            c& = _RGB32(Asc(palette$, i * 3 + 1), Asc(palette$, i * 3 + 2), Asc(palette$, i * 3 + 3))
            _PaletteColor i, c&
        Next
    End If

    Get file, , byte ' minimumCodeSize

    Do
        GoSub GetCode
        stackPointer = 0
        If code = clearCode Then 'Reset & Draw next color direct
            nvc = minCode '           \
            codeSize = startCodeSize ' Preset default codes
            maxCode = startMaxCode '  /

            GoSub GetCode
            currentCode = code

            lastColor = code
            colorStack(stackPointer) = lastColor
            stackPointer = 1
        ElseIf code <> endCode Then 'Draw direct color or colors from suffix
            currentCode = code
            If currentCode = nvc Then 'Take last color too
                currentCode = oldCode
                colorStack(stackPointer) = lastColor
                stackPointer = stackPointer + 1
            End If

            While currentCode >= minCode 'Extract colors from suffix
                colorStack(stackPointer) = suffix(currentCode)
                stackPointer = stackPointer + 1
                currentCode = prefix(currentCode) 'Next color from suffix is described in
            Wend '                                 the prefix, else prefix is the last col.

            lastColor = currentCode '              Last color is equal to the
            colorStack(stackPointer) = lastColor ' last known code (direct, or from
            stackPointer = stackPointer + 1 '      Prefix)
            suffix(nvc) = lastColor 'Automatically, update suffix
            prefix(nvc) = oldCode 'Code from the session before (for extracting from suffix)
            nvc = nvc + 1

            If nvc > maxCode And codeSize < 12 Then
                codeSize = codeSize + 1
                maxCode = maxCode * 2 + 1
            End If
        End If

        For i = stackPointer - 1 To 0 Step -1
            PSet (x, y), colorStack(i)
            x = x + 1
            If x = framedata.width Then
                x = 0
                If framedata.interlacedFlag Then
                    y = y + interlacedStep
                    If y >= framedata.height Then
                        Select Case interlacedPass
                            Case 0: interlacedPass = 1: y = 4
                            Case 1: interlacedPass = 2: y = 2
                            Case 2: interlacedPass = 3: y = 1
                        End Select
                        interlacedStep = 2 * y
                    End If
                Else
                    y = y + 1
                End If
            End If
        Next

        oldCode = code
    Loop Until code = endCode

    Get file, , byte
    Exit Sub

    GetCode:
    If bitPointer = 0 Then GoSub ReadByteFromBlock: bitPointer = 8
    WorkCode& = LastChar \ powerOf2&(8 - bitPointer)
    While codeSize > bitPointer
        GoSub ReadByteFromBlock

        WorkCode& = WorkCode& Or LastChar * powerOf2&(bitPointer)
        bitPointer = bitPointer + 8
    Wend
    bitPointer = bitPointer - codeSize
    code = WorkCode& And maxCode
    Return

    ReadByteFromBlock:
    If blockPointer = blockSize Then
        Get file, , byte: blockSize = byte
        a$ = Space$(blockSize): Get file, , a$
        blockPointer = 0
    End If
    blockPointer = blockPointer + 1
    LastChar = Asc(Mid$(a$, blockPointer, 1))
    Return
End Sub

' TODO - GIF sub (https://qb64.com/wiki/GIF-Images)
Sub codeGif (gifData As GIFDATA, frameData() As FRAMEDATA)
    For i = 0 To UBound(frameData)
        If frameData(i).addr < 0 Then _FreeImage frameData(i).addr
    Next

    Close gifData.file
End Sub

' Display the GIF page
'
' Parameters:
' game - Book instance
Sub DisplayGIFPage (game As Book)
    Call TurnPage(game, 1)
    Call DisplayGIF(game, game.openingGIF)
End Sub

' Display the high scores page
'
' Parameters:
' game - Book instance
Sub DisplayHighScoresPage (game As Book)
    Call TurnPage(game, 1)

    If game.highScores <> "" Then
        Dim scene As Scene
        scene.name = "HIGH SCORES"
        scene.color = _RGB(0, 0, 0)
        Call DisplayStoryPage(game, scene, game.highScores, "", 1)
    Else
        Call PrintPageText(game, "story", CsrLin, "N/A")
    End If

    If game.isOver = 0 Then
        Call PressAnyKey(game, 0)
    End If
End Sub

' Display an image on a designated book page
'
' Parameters:
' game - Book instance
' page - (STORY/CONTROL) Which page of the book to use
' image - Image to display
Sub DisplayImage (game As Book, page As String, image As Long)
    ' Determine left edge of the designated page
    page = UCase$(page)
    If ((page = "STORY") And (game.isRightHanded = 1)) Or ((page = "CONTROL") And (game.isRightHanded = 0)) Then
        leftColumn% = game.leftPageStartingColumn
    Else
        If ((page = "STORY") And (game.isRightHanded = 0)) Or ((page = "CONTROL") And (game.isRightHanded = 1)) Then
            leftColumn% = game.rightPageStartingColumn
        Else
            If 1 / 0 Then
                ' Let the programmer know that they dorked things
            End If
        End If
    End If

    ' Calculate the top-left edge of the image
    centerColumn% = Int(leftColumn% + (game.maxPageLength / 2)) ' Calculate the middle column on the page
    imageWidth% = _Width(image)
    imageHeight% = _Height(image)
    imageColumn% = centerColumn% - Int((imageWidth% / _FontWidth) / 2) - 1
    imageRow% = CsrLin
    imageX% = imageColumn% * _FontWidth
    imageY% = imageRow% * _FontHeight
    _PutImage (imageX%, imageY%), image

    ' Track the calculated image coordinates
    game.lastImageX = imageX%
    game.lastImageY = imageY%

    ' Move the cursor to the next row after the image
    nextRow% = imageRow% + _Ceil(imageHeight% / _FontHeight) + 1
    Locate nextRow%, leftColumn%
End Sub

Sub DisplayScene (game As Book, scene As Scene)
    ' TODO - For each page in a scene, call DisplayStoryPage()
End Sub

' Display the select difficulty page
'
' Parameters:
' game - Book instance
Sub DisplaySelectDifficultyPage (game As Book)
    turnPageSound% = 1
    Do
        Call TurnPage(game, turnPageSound%)
        game.difficulty = StoryPrompt$(game, "Difficulty Choices", EASY + "," + HARD, 0, 1, 4)
        turnPageSound% = 0 ' If the player enters an incorrect choice and the page is turned to reset the screen, be silent
    Loop While Not Contains%(game.storyChoices, game.difficulty, ",")
End Sub

' Display the select player page
'
' Parameters:
' game - Book instance
Sub DisplaySelectPlayerPage (game As Book)
    ' TODO - Add the ability to hover over a portion of the image to highlight one of the players and let a mouse click auto-select that player
    ' TODO - Display a brief description of the highlighted player
    selectPlayerImage& = _LoadImage("select-player.png")
    turnPageSound% = 1

    Do
        Call TurnPage(game, turnPageSound%)
        Call DisplayImage(game, "story", selectPlayerImage&)
        game.player = StoryPrompt$(game, "Player Choices", GREYSON + "," + ZARA, 0, 1, 7)
        turnPageSound% = 0 ' If the player enters an incorrect choice and the page is turned to reset the screen, be silent
    Loop While Not Contains%(game.storyChoices + "," + GREY, game.player, ",")

    If game.player = ZARA Then
        game.twin = GREYSON
    Else
        game.twin = ZARA
    End If

    _FreeImage selectPlayerImage&
End Sub

' Display the splash screen
'
' Parameters:
' game - Book instance
Sub DisplaySplashScreen (game As Book)
    ' Play Wayne Chung's theme song
    game.storySound = PlaySound&("wayne-chung.mp3", 1, 0)
    ' Display Wayne Chung's picture
    Cls
    wayneChungImage& = _LoadImage("wayne-chung.jpg", 32) 'load the image file to be drawn
    Screen wayneChungImage&
    _ScreenMove ((_DesktopWidth \ 2) - (_Width \ 2)) - 3, ((_DesktopHeight \ 2) - (_Height \ 2)) - 29 - 400 ' Center the application window (400 offsets two stacked monitors)
    ' Display overlay text
    Color _RGB(0, 0, 0)
    _PrintMode _KeepBackground
    Locate 7, 2
    Print "Wayne Chung Enterprises"
    Locate 9, 9
    Print "presents"
    Locate 46, 2
    ' Suspend program execution for 5 seconds or until the user presses the any key
    Sleep 5
    ' Stop playing Wayne Chung's theme music and remove his picture (New screen must be set without Chung's picture before it is free'ed)
    StopSound game.storySound

    ' Display the book picture so Wayne Chung's picture can be freed from memory
    Screen _NewImage(1280, 793, 32)
    Cls , _RGB(0, 0, 0)
    _PutImage , game.image
    _FreeImage wayneChungImage& ' Free up memory used by the image
End Sub

' Display a single story page
'
' Parameters:
' game - Book instance
' scene - Scene instance
' page - Story text and supported tags
' [action] - Pre-selected action
' [startAtTop] - Start displaying at the top of the story page? (1/0)
Sub DisplayStoryPage (game As Book, scene As Scene, page As String, action As String, startAtTop As Integer)
    Dim soundsFiles$(1 To 100)
    Dim soundsHandles&(1 To 100)
    Dim variable As Variable
    soundsCount% = 0
    subsection$ = ""
    startingAction$ = action

    If scene.color = _RGB(255, 255, 255) Then
        Color _RGB(0, 0, 0) ' For readability, never use white text on the book
    Else
        Color scene.color
    End If
    If startAtTop Then
        Locate game.firstRow + 2, 1
    End If

    If action = "LOOK" Then
        ' Let the player know where they are
        Color _RGB(0, 0, 0)
        Call PrintPageText(game, "story", CsrLin, "You are in ")
        If scene.color = _RGB(255, 255, 255) Then
            Color _RGB(0, 0, 0) ' For readability, never use white text on the book
        Else
            Color scene.color
        End If
        Locate CsrLin - 1, game.cursor.lastColumn + Len("You are in ")
        Print scene.name
        Color _RGB(0, 0, 0)
        Locate CsrLin - 1, game.cursor.lastColumn + Len("You are in " + scene.name)
        Print "."

        ' Let the player know what lies ahead
        aheadX% = game.playerX
        aheadY% = game.playerY
        If game.playerDirection = game.directions.north Then
            aheadY% = aheadY% - 1
        End If
        If game.playerDirection = game.directions.east Then
            aheadX% = aheadX% + 1
        End If
        If game.playerDirection = game.directions.south Then
            aheadY% = aheadY% + 1
        End If
        If game.playerDirection = game.directions.west Then
            aheadX% = aheadX% - 1
        End If
        Color _RGB(0, 0, 0)
        Dim nextScene As Scene
        Call WorldScene(RETRIEVE, 1, game.level, aheadX%, aheadY%, nextScene)
        If nextScene.name = "" Then
            Color _RGB(0, 0, 0)
            Call PrintPageText(game, "story", CsrLin, "There is nothing ahead.  You are facing the edge of the world.")
        Else
            Call PrintPageText(game, "story", CsrLin, "Ahead you see ")
            If nextScene.color = _RGB(255, 255, 255) Then
                Color _RGB(0, 0, 0) ' For readability, never use white text on the book
            Else
                Color nextScene.color
            End If
            Locate CsrLin - 1, game.cursor.lastColumn + Len("Ahead you see ")
            Print nextScene.name
            Color _RGB(0, 0, 0)
            Locate CsrLin - 1, game.cursor.lastColumn + Len("Ahead you see " + nextScene.name)
            Print "."
        End If

        If scene.color = _RGB(255, 255, 255) Then
            Color _RGB(0, 0, 0) ' For readability, never use white text on the book
        Else
            Color scene.color
        End If

        Print
    End If

    divCount% = ListLength%(page, "<div>")
    isExitSet% = 0
    For i% = 1 To divCount%
        div$ = ListPiece$(page, "<div>", i%)
        div$ = LTrim$(RTrim$(div$))
        divListLength% = Len(div$)
        storyText$ = ""
        remainingDiv$ = div$
        'If action <> "" Then
        If Left$(remainingDiv$, 2) = "[[" Then
            subsection$ = ListPiece$(remainingDiv$, "]]", 1)
            subsection$ = ListPiece$(subsection$, "[[", 2)
            _Continue ' Don't actually print the name of the subsection
        End If
        If subsection$ <> action Then
            _Continue
        End If
        'End If
        ' Process all tags in the division
        Do
            tag$ = GetTag$(remainingDiv$, tagStart%, tagEnd%, tagValue$)

            If tag$ <> "" Then
                storyText$ = storyText$ + Left$(remainingDiv$, tagStart% - 1)
                remainingDiv$ = Mid$(remainingDiv$, tagEnd% + 1, Len(remainingDiv$))

                ' Supported Tags:
                ' <br>/<break>
                ' <choices-add CHOICE1+CHOICE2+CHOICE3...>
                ' <choices-remove CHOICE1+CHOICE2+CHOICE3...>
                ' <color RED+BLUE+GREEN/STORY>
                ' <end-page>
                ' <get-input LABEL:CHOICE1+CHOICE2+CHOICE3...>
                ' <get-story-variable VARIABLE>
                ' <goto-chapter CHAPTER>
                ' <hp-remove POINTS>
                ' <if-game-variable PLAYER/TODO>
                ' <if-inventory-contains ITEM>
                ' <if-story-variable VARIABLE>
                ' <image IMAGE1+IMAGE2+IMAGE3...>
                ' <inventory-add ITEM1+ITEM2+ITEM3...>
                ' <load-scene>
                ' <monster-shooter>
                ' <play-sound SOUND1+SOUND2+SOUND3...>
                ' <player>
                ' <press-any-key>
                ' <quote>
                ' <random COUNT>
                ' <score-add POINTS>
                ' <set-game-variable VARIABLE=VALUE>
                ' <set-player-mode VALUE>
                ' <set-story-variable VARIABLE=VALUE>
                ' <stop-sound SOUND1+SOUND2+SOUND3...>
                ' <story-prompt LABEL:CHOICE1+CHOICE2+CHOICE3...>
                ' <story-sound SOUND>
                ' <story-variable VARIABLE>
                ' <turn-page>
                ' <twin>
                ' <unbreak>

                If (tag$ = "br") Or (tag$ = "break") Then
                    ' The Break tag prints any accumulated text and advances to the next line
                    ' TODO - PrintPageText() now natively supports <br>
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    Else
                        Locate CsrLin + 1, 1
                    End If
                    _Continue
                End If
                If tag$ = "choices-add" Then
                    ' The Choices-Add tag adds the choice(s) to the main story choices list
                    choiceCount% = ListLength%(tagValue$, "+")
                    For j% = 1 To choiceCount%
                        newChoice$ = ListPiece$(tagValue$, "+", j%)
                        game.storyChoices = ListAdd$(game.storyChoices, ",", newChoice$)
                    Next j%
                    _Continue
                End If
                If tag$ = "choices-remove" Then
                    ' The Choices-Remove tag removes the choice(s) from the main story choices list
                    choiceCount% = ListLength%(tagValue$, "+")
                    For j% = 1 To choiceCount%
                        newChoice$ = ListPiece$(tagValue$, "+", j%)
                        game.storyChoices = ListRemove$(game.storyChoices, ",", newChoice$)
                    Next j%
                    _Continue
                End If
                If tag$ = "color" Then
                    ' The Color tag prints any accumulated text and then changes the font color to the designated RGB value or resets to the scene's story color
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    If tagValue$ = "STORY" Then
                        If scene.color = _RGB(255, 255, 255) Then
                            Color _RGB(0, 0, 0) ' For readability, never use white text on the book
                        Else
                            Color scene.color
                        End If
                    Else
                        redValue% = Val(ListPiece$(tagValue$, "+", 1))
                        greenValue% = Val(ListPiece$(tagValue$, "+", 2))
                        blueValue% = Val(ListPiece$(tagValue$, "+", 3))
                        Color _RGB(redValue%, greenValue%, blueValue%)
                    End If
                    _Continue
                End If
                If tag$ = "end-page" Then
                    ' The End-Page tag prints any accumulated text and then causes an immediate termination of the page
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    isExitSet% = 1
                    Exit Do
                End If
                If tag$ = "get-input" Then
                    ' The Get-Input tag prints any accumulated text and prompts the user for an input string
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    inputLabel$ = ListPiece(tagValue$, ":", 1)
                    inputChoices$ = ListPiece(tagValue$, ":", 2)
                    userInput$ = GetInput$(game, inputLabel$, 0, 0, 40)
                    If ListContains%(inputChoices$, "+", userInput$) Then
                        action = "INPUT " + userInput$
                    Else
                        action = "INPUT ELSE"
                    End If
                    If scene.color = _RGB(255, 255, 255) Then
                        Color _RGB(0, 0, 0) ' For readability, never use white text on the book
                    Else
                        Color scene.color
                    End If
                    _Continue
                End If
                If tag$ = "get-story-variable" Then
                    ' The Get-Story-Variable tag adds the value of the target variable to the story text
                    variable.name = tagValue$
                    Call StoryVariable(RETRIEVE, variable)
                    storyText$ = storyText$ + variable.value
                    _Continue
                End If
                If tag$ = "goto-chapter" Then
                    ' The Goto-Chapter tag redirects the player to the designated chapter
                    game.chapter = "CHAPTER " + tagValue$
                    _Continue
                End If
                If tag$ = "hp-remove" Then
                    ' The HP-Remove tag prints any accumulated text and decreases the player's hit points
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    Call DecreaseHitPoints(game, Val(tagValue$), 1)
                    If scene.color = _RGB(255, 255, 255) Then
                        Color _RGB(0, 0, 0) ' For readability, never use white text on the book
                    Else
                        Color scene.color
                    End If
                    _Continue
                End If
                If tag$ = "if-game-variable" Then
                    ' The If-Game-Variable tag prints any accumulated text and then sets the current action to the sub-section that matches <VARIABLE> <VALUE>
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    variable$ = tagValue$
                    variableValue$ = ""
                    If variable$ = "PLAYER" Then
                        variableValue$ = game.player
                    End If
                    action = (variable$ + " " + variableValue$)
                    _Continue
                End If
                If tag$ = "if-inventory-contains" Then
                    ' The If-Inventory-Contains tag prints any accumulated text and then sets the current action to the sub-section that matches HAS <ITEM> or NO <ITEM>
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    item$ = tagValue$
                    If ListContains%(game.inventory, ",", item$) = 1 Then
                        action = "HAS " + item$
                    Else
                        action = "NO " + item$
                    End If
                    _Continue
                End If
                If tag$ = "if-story-variable" Then
                    ' The If-Story-Variable tag prints any accumulated text and then sets the current action to the sub-section that matches <VARIABLE> <VALUE>
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    variable.name = tagValue$
                    Call StoryVariable(RETRIEVE, variable)
                    action = (variable.name + " " + variable.value)
                    _Continue
                End If
                If tag$ = "image" Then
                    ' The Image tag prints any accumulated text and displays the image(s) on the next line
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    imageLine% = CsrLin
                    imageCount% = ListLength%(tagValue$, "+")
                    For j% = 1 To imageCount%
                        imageFile$ = ListPiece$(tagValue$, "+", j%)
                        image& = _LoadImage(imageFile$)
                        Locate imageLine%, 1
                        Call DisplayImage(game, "story", image&)
                        _FreeImage image&
                    Next j%
                    _Continue
                End If
                If tag$ = "inventory-add" Then
                    ' The Inventory-Add tag prints any accumulated text and adds the item(s) to the player's inventory
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    itemCount% = ListLength%(tagValue$, "+")
                    For j% = 1 To itemCount%
                        item$ = ListPiece$(tagValue$, "+", j%)
                        Call IncreaseInventory(game, item$)
                    Next j%
                    If scene.color = _RGB(255, 255, 255) Then
                        Color _RGB(0, 0, 0) ' For readability, never use white text on the book
                    Else
                        Color scene.color
                    End If
                    _Continue
                End If
                If tag$ = "load-scene" Then
                    ' The Load-Scene tag loads a new story scene
                    ' TODO - This is a VERY ugly way to load a scene.  Could be cleaned up.
                    Dim newScene As Scene
                    Call WorldScene(RETRIEVE, 1, game.level, game.playerX, game.playerY, newScene)
                    Call LoadScene(game, newScene)
                    Call TurnPage(game, 1)
                    Call LookAction(game)
                    isExitSet% = 1
                    _Continue
                End If
                If (tag$ = "monster-shooter") Then
                    ' The Choices-Add tag prints any accumulated text and then launches the experimental arcade shooter mode
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    success% = 0
                    keepPlaying% = 1
                    startingHP% = game.hitPoints
                    backgroundImageFile$ = ListPiece$(tagValue$, "+", 1)
                    storySoundFile$ = ListPiece$(tagValue$, "+", 2)
                    playerImageFile$ = ListPiece$(tagValue$, "+", 3)
                    missileLeftImageFile$ = ListPiece$(tagValue$, "+", 4)
                    missileRightImageFile$ = ListPiece$(tagValue$, "+", 5)
                    monsterImageFile$ = ListPiece$(tagValue$, "+", 6)
                    isMonsterVisible% = Val(ListPiece$(tagValue$, "+", 7))
                    monsterSoundFile$ = ListPiece$(tagValue$, "+", 8)
                    While keepPlaying% = 1
                        success% = MonsterShooter%(game, backgroundImageFile$, storySoundFile$, playerImageFile$, missileLeftImageFile$, missileRightImageFile$, monsterImageFile$, isMonsterVisible%, monsterSoundFile$)
                        Call PressAnyKey(game, 1)
                        keepPlaying% = 0
                        If success% = 0 Then
                            Call TurnPage(game, 1)
                            promptLabel$ = "Defeated By Boss"
                            formattedChoices$ = "Abort,Retry,Fail"
                            promptChoicesCount% = ListLength%(promptChoices$, "+")
                            Locate CsrLin + 1, 1
                            ' TODO - Enable arrow keys if normal level chapter.  This is being passed as 0.
                            userInput$ = StoryPrompt$(game, promptLabel$, formattedChoices$, 0, 1, 5)
                            Locate CsrLin + 2, 1
                            If userInput$ = "Retry" Then
                                keepPlaying% = 1
                                game.hitPoints = startingHP%
                            End If
                        End If
                    Wend
                    ' Automatically redirect to a section for handling the success or failure of the minigame
                    If success% = 1 Then
                        action = "[[MINIGAME VICTORY]]"
                    Else
                        action = "[[MINIGAME DEFEAT]]"
                    End If
                    _Continue
                End If
                If tag$ = "play-sound" Then
                    ' The Play-Sound tag prints any accumulated text and plays the new sound(s)
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    soundCount% = ListLength%(tagValue$, "+")
                    For j% = 1 To soundCount%
                        soundFile$ = ListPiece$(tagValue$, "+", j%)
                        soundHandle& = PlaySound&(soundFile$, 1, 0)
                        soundsCount% = soundsCount% + 1
                        soundsFiles$(soundsCount%) = soundFile$
                        soundsHandles&(soundsCount%) = soundHandle&
                    Next j%
                    _Continue
                End If
                If tag$ = "player" Then
                    ' The Player tag is translated into the name of the game's player
                    storyText$ = storyText$ + game.player
                    _Continue
                End If
                If tag$ = "press-any-key" Then
                    ' The Press-Any-Key tag prints any accumulated text and then causes the program to sleep until the player provides input
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    Call PressAnyKey(game, 1)
                    _Continue
                End If
                If tag$ = "quote" Then
                    ' The Quote tag is translated into the double quote character
                    storyText$ = storyText$ + Chr$(34)
                    _Continue
                End If
                If tag$ = "random" Then
                    ' The Random tag prints any accumulated text and sets the current action to a sub-section matching the random value
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    Randomize Timer
                    randomNumber% = Int(Rnd * Val(tagValue$)) + 1
                    action = "RANDOM " + LTrim$(Str$(randomNumber%))
                    _Continue
                End If
                If tag$ = "score-add" Then
                    ' The Score-Add tag prints any accumulated text and increases the player's score
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    Call IncreaseScore(game, Val(tagValue$))
                    If scene.color = _RGB(255, 255, 255) Then
                        Color _RGB(0, 0, 0) ' For readability, never use white text on the book
                    Else
                        Color scene.color
                    End If
                    _Continue
                End If
                If tag$ = "set-player-mode" Then
                    ' The Set-Player-Mode tag prints any accumulated text and sets the game's player mode to the designated value
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    Call SetPlayerMode(game, tagValue$)
                    _Continue
                End If
                If tag$ = "set-game-variable" Then
                    ' The Set-Game-Variable tag sets the target variable to the designated value
                    variableName$ = ListPiece$(tagValue$, "=", 1)
                    variableValueStr$ = ListPiece$(tagValue$, "=", 2)
                    If variableName$ = "playerX" Then
                        If variableValueStr$ = "LAST" Then
                            variableValue% = game.playerLastX
                        Else
                            variableValue% = Val(variableValueStr$)
                        End If
                        If game.playerX <> variableValue% Then
                            game.playerLastX = game.playerX
                            game.playerX = variableValue%
                        End If
                        _Continue
                    End If
                    If variableName$ = "playerY" Then
                        If variableValueStr$ = "LAST" Then
                            variableValue% = game.playerLastY
                        Else
                            variableValue% = Val(variableValueStr$)
                        End If
                        If game.playerY <> variableValue% Then
                            game.playerLastY = game.playerY
                            game.playerY = variableValue%
                        End If
                        _Continue
                    End If
                    If variableName$ = "level" Then
                        If variableValueStr$ = "LAST" Then
                            variableValue% = game.lastLevel
                        Else
                            variableValue% = Val(variableValueStr$)
                        End If
                        If game.level <> variableValue% Then
                            game.lastLevel = game.level
                            game.level = variableValue%
                        End If
                        _Continue
                    End If
                    ' TODO - Implement more game variables
                    _Continue
                End If
                If tag$ = "set-story-variable" Then
                    ' The Set-Story-Variable tag sets the target variable to the designated value
                    variable.name = ListPiece$(tagValue$, "=", 1)
                    variable.value = ListPiece$(tagValue$, "=", 2)
                    Call StoryVariable(UPDATE, variable)
                    _Continue
                End If
                If tag$ = "stop-sound" Then
                    ' The Stop-Sound tag prints any accumulated text and stops the playing sound(s)
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    soundCount% = ListLength%(tagValue$, "+")
                    For j% = 1 To soundCount%
                        soundFile$ = ListPiece$(tagValue$, "+", j%)
                        For k% = 1 To soundsCount%
                            If soundsFiles$(k%) = soundFile$ Then
                                soundHandle& = soundsHandles&(k%)
                                Call StopSound(soundHandle&)
                            End If
                        Next k%
                    Next j%
                    _Continue
                End If
                If tag$ = "story-prompt" Then
                    ' The Story-Prompt tag prints any accumulated text and prompts the user with a list of choices
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    promptLabel$ = ListPiece$(tagValue$, ":", 1)
                    promptChoices$ = ListPiece$(tagValue$, ":", 2)
                    promptChoicesCount% = ListLength%(promptChoices$, "+")
                    formattedChoices$ = ""
                    maxChoiceListLength% = 0
                    For j% = 1 To promptChoicesCount%
                        choice$ = ListPiece$(promptChoices$, "+", j%)
                        choiceListLength% = Len(choice$)
                        If choiceListLength% > maxChoiceListLength% Then
                            maxChoiceListLength% = choiceListLength%
                        End If
                        If formattedChoices$ <> "" Then
                            formattedChoices$ = formattedChoices$ + ","
                        End If
                        formattedChoices$ = formattedChoices$ + choice$
                    Next j%
                    If maxChoiceListLength% > 40 Then
                        ' TODO - Hopefully this never happens!
                        maxChoiceListLength% = 40
                    End If
                    Locate CsrLin + 1, 1
                    ' TODO - Enable arrow keys if normal level chapter.  This is being passed as 0.
                    action = StoryPrompt$(game, promptLabel$, formattedChoices$, 0, 1, maxChoiceListLength%)
                    Locate CsrLin + 2, 1

                    ' The remaining page text should now only be the sub-section for the selected choice, headed by [[choice]], and
                    ' ending with the next choice's header or the end of the page text, whichever comes first
                    ' TODO - The main for-loop needs to be updated to skip any <div> not in the selected choice's sub-section
                    ' TODO - It's kind of magical that any remaining text in the div will be processed and then subsequent divs skipped if they
                    ' don't match the target sub-section
                    _Continue
                End If
                If tag$ = "story-sound" Then
                    ' The Story-Sound tag prints any accumulated text, stops the current story sound, and then plays the new story sound in a loop
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    soundFile$ = tagValue$
                    Call StopSound(game.storySound)
                    game.storySound = PlaySound&(soundFile$, 1, 1)
                    _Continue
                End If
                ' TODO - This looks like a duplicate of get-story-variable.  Which implementation is better?
                If tag$ = "story-variable" Then
                    ' The Story-Variable tag prints any accumulated text and then prints the value of the variable
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    variable.name = tagValue$
                    Call StoryVariable(RETRIEVE, variable)
                    Call PrintPageText(game, "story", CsrLin, variable.value)
                    _Continue
                End If
                If tag$ = "turn-page" Then
                    ' The Next-Page tag advances to the next page in the story scene
                    game.pageNumber = game.pageNumber + 1
                    _Continue
                End If
                If tag$ = "twin" Then
                    ' The Twin tag is translated into the name of the game player's twin
                    storyText$ = storyText$ + game.twin
                    _Continue
                End If
                If tag$ = "unbreak" Then
                    ' The Unbreak tag prints any accumulated text and advances to the previous line
                    currentLine% = CsrLin
                    If storyText$ <> "" Then
                        Call PrintPageText(game, "story", CsrLin, storyText$)
                        storyText$ = ""
                    End If
                    Locate currentLine% - 1, 1
                    _Continue
                End If

                storyText$ = storyText$ + tag$ ' Not a supported tag so just show its text
            Else
                storyText$ = storyText$ + remainingDiv$
                remainingDiv$ = ""
                If storyText$ <> "" Then
                    Call PrintPageText(game, "story", CsrLin, storyText$)
                End If
            End If
        Loop Until tag$ = ""
        If isExitSet% = 1 Then
            Exit For
        End If
    Next i%

    ' Print any remaining story text
    If storyText$ <> "" Then
        Call PrintPageText(game, "story", CsrLin, storyText$)
        storyText$ = ""
    End If

    ' Reset the default font color
    Color _RGB(0, 0, 0)

    If startingAction$ = "LOOK" Then
        Print
        Call DisplayStoryPage(game, scene, page, "", 0)
    End If

    ' Stop all current play-once sounds
    For i% = 1 To soundsCount%
        soundHandle& = soundsHandles&(i%)
        Call StopSound(soundHandle&)
    Next i%
End Sub

' Display the title page
'
' Parameters:
' game - Book instance
Sub DisplayTitlePage (game As Book)
    Call TurnPage(game, 1)
    Color _RGB(139, 0, 0) ' dark red
    ' TODO - Since TurnPage() moves the cursor to row 5, can the row parameters all just be CsrLin?
    Call PrintPageText(game, "story", CsrLin, "                                   ___, ____--'")
    Call PrintPageText(game, "story", CsrLin, "                              _,-.'_,-'      (")
    Call PrintPageText(game, "story", CsrLin, "                           ,-' _.-''....____(")
    Call PrintPageText(game, "story", CsrLin, "                 ,))_     /  ,'\ `'-.     (          /\")
    Call PrintPageText(game, "story", CsrLin, "         __ ,+..a`  \(_   ) /   \    `'-..(         /  \")
    Call PrintPageText(game, "story", CsrLin, "         )`-;...,_   \(_ ) /     \  ('''    ;'^^`\ <./\.>")
    Call PrintPageText(game, "story", CsrLin, "             ,_   )   |( )/   ,./^``_..._  < /^^\ \_.))")
    Call PrintPageText(game, "story", CsrLin, "            `=;; (    (/_')-- -'^^`      ^^-.`_.-` >-'")
    Call PrintPageText(game, "story", CsrLin, "            `=\\ (                             _,./")
    Call PrintPageText(game, "story", CsrLin, "              ,\`(                         )^^^")
    Call PrintPageText(game, "story", CsrLin, "                ``;         __-'^^\       /")
    Call PrintPageText(game, "story", CsrLin, "                  / _>---^^^   `\..`-.    ``'.")
    Call PrintPageText(game, "story", CsrLin, "                 / /               / /``'`; /")
    Call PrintPageText(game, "story", CsrLin, "                / /          ,-=='-`=-'  / /")
    Call PrintPageText(game, "story", CsrLin, "          ,-=='-`=-.               ,-=='-`=-.")
    Color _RGB(0, 0, 0) ' black
    Call PrintPageText(game, "story", CsrLin, "        *******************************************")
    Call PrintPageText(game, "story", CsrLin, "                    T W I N   Q U E S T")
    halfStoryLen% = _Round(Len(game.story) / 2)
    Call PrintPageText(game, "story", CsrLin + 1, "        " + String$(22 - halfStoryLen%, " ") + game.story)
    Call PrintPageText(game, "story", CsrLin + 2, "                            b y")
    halfAuthorLen% = _Round(Len(game.author) / 2)
    Call PrintPageText(game, "story", CsrLin + 1, "        " + String$(22 - halfAuthorLen%, " ") + game.author)
    Print
    Print
    Call PressAnyKey(game, 0)
End Sub

Sub DumbAction (game As Book)
    Call PrintPageText(game, "story", CsrLin, "That is NOT a choice.  Better luck next time.")
    Print
End Sub

Sub ExitAction (game As Book)
    game.isOver = 1
    If Contains%(game.inventory, "GOLD", ",") Then
        Call PrintPageText(game, "story", CsrLin, "But you were doing so well!  You could have tried returning the GOLD to the elevator.")
        Call PrintPageText(game, "story", CsrLin, "You found the GOLD but didn't bring it home.")
    End If
    Call PrintPageText(game, "story", CsrLin, "Your quest has ended... no fame.  And certainly no fortune.")
    Call PrintPageText(game, "story", CsrLin, "REMEMBER: Quitters never win.")
End Sub

' Get input from the user
'
' Parameters:
' game - Book instance
' prompt - Text to display
' enableArrowKeys - Enable use of the keyboard arrow keys? (1/0)
'                   Up - "MOVE AHEAD"
'                   Down - "MOVE BACKWARDS"
'                   Left - "TURN LEFT"
'                   Right - "TURN RIGHT"
' enableMouse - Enable use of the mouse? (1/0)
' maxLength - Maximum number of characters to input
'
' Returns:
' User input
Function GetInput$ (game As Book, prompt As String, enableArrowKeys As Integer, enableMouse As Integer, maxLength As Integer)
    ' TODO - When printing text on an image, the runtime won't keep track of the image area being printed on and there is no clean way
    ' to remove text without implementing something that does this via a command like GET that copies each slice of a region that could
    ' be printed on so it can be redisplayed later.
    inp$ = ""
    While _MouseInput <> 0 ' Read the mouse input until there is no more left to clear previous clicks
    Wend
    highlightedChoice$ = ""
    highlightedChoiceRow% = 0
    highlightedChoiceCol% = 0
    If enableMouse Then
        ' TODO - This is an ugly work-around to not being able to section out the mouse calculations into a subroutine
        fakeInput% = 1
    Else
        fakeInput% = 0
    End If
    cursor$ = Chr$(178)
    currentRow% = CsrLin
    If game.isRightHanded Then
        startingColumn% = game.leftPageStartingColumn ' Story page is the left-hand page
    Else
        startingColumn% = game.rightPageStartingColumn ' Story page is the right-hand page
    End If
    Color _RGB(0, 0, 0) ' black
    Locate currentRow%, startingColumn%
    Print prompt + ": "
    _PrintMode _FillBackground ' Replace background instead of displaying just text over the image
    Color _RGB(255, 255, 255), _RGB(255, 255, 255) ' white on white
    Locate currentRow%, startingColumn% + Len(prompt + ": ")
    Print String$(maxLength + 1, " ") ' Print spaces to clear the field and make all white so that a white block can be used to erase characters
    currentCol% = startingColumn% + Len(prompt + ": ")
    Locate currentRow%, currentCol%
    Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan
    Print cursor$
    c$ = ""
    While c$ <> Chr$(13)
        c$ = ""
        While (c$ = "") And (Not _MouseInput) And (Not fakeInput%)
            c$ = InKey$
        Wend
        ' If InKey$ is still empty, that means _MouseInput is set
        If c$ = "" Then
            ' Handle the mouse event
            If enableMouse Then
                If fakeInput% = 1 Then
                    fakeInput% = 0
                End If
                ' Calculate the row and column of the current mouse position
                mouseRow% = _Ceil(_MouseY / _FontHeight)
                mouseCol% = _Ceil(_MouseX / _FontWidth)
                ' The possible choices are limited to the current row
                possibleChoices$ = ""
                If (mouseRow% = game.controlChoicesRow) Then
                    possibleChoices$ = game.controlChoices
                    possibleChoicesCol% = game.controlChoicesColumn
                Else
                    If (mouseRow% = game.storyChoicesRow) Then
                        possibleChoices$ = game.storyChoices
                        possibleChoicesCol% = game.storyChoicesColumn
                    End If
                End If
                'Print "mouseRow%=" + Str$(mouseRow%) + ", mouseCol%=" + Str$(mouseCol%) + ", game.storyChoicesRow=" + Str$(game.storyChoicesRow) + ", game.storyChoicesColumn=" + Str$(game.storyChoicesColumn) + ", game.storyChoices=" + game.storyChoices
                ' Evaluate whethere there are possible choices on the current row and the mouse cursor's column position is in the range of the possible choices
                choice$ = ""
                If (possibleChoices$ <> "") And (mouseCol% >= possibleChoicesCol%) And (mouseCol% <= (possibleChoicesCol% + Len(possibleChoices$) - 1)) Then
                    choicePosition% = mouseCol% - possibleChoicesCol% + 1 ' Get the exact character position of the current choices string... 1 to the length of the choices
                    If Mid$(possibleChoices$, choicePosition%, 1) <> "," Then
                        ' Calculate rightmost character position of the current choice by looking for the next comma
                        choiceRightPos% = InStr(choicePosition%, possibleChoices$, ",")
                        If choiceRightPos% = 0 Then
                            ' There was no comma to the right of the current character so this must be the final choice
                            choiceRightPos% = Len(possibleChoices$)
                        Else
                            ' The rightmost character is 1 less than the position of the comma
                            choiceRightPos% = choiceRightPos% - 1
                        End If
                        ' Calculate leftmost character position of the current choice by looking for the previous comma
                        choiceLeftPos% = _InStrRev(choicePosition%, possibleChoices$, ",")
                        If choiceLeftPos% = 0 Then
                            ' There was no comma to the left of the current character so this must be the first choice
                            choiceLeftPos% = 1
                        Else
                            ' The leftmost character is 1 greater than the position of the comma
                            choiceLeftPos% = choiceLeftPos% + 1
                        End If
                        ' Extract the current choice using the leftmost and rightmost character positions
                        choice$ = Mid$(possibleChoices$, choiceLeftPos%, choiceRightPos% - choiceLeftPos% + 1)
                        If _MouseButton(1) = -1 Then
                            ' Handle a click using the left mouse button
                            inp$ = choice$
                            c$ = Chr$(13)
                            Locate currentRow%, currentCol%
                            Color _RGB(0, 0, 0), _RGB(255, 255, 255) ' black on white
                            Print inp$
                        Else
                            ' User only hovered over a new choice
                            If highlightedChoice$ <> choice$ Then
                                If highlightedChoice$ <> "" Then
                                    ' Remove the highlight from the last highlighted choice
                                    Locate highlightedChoiceRow%, highlightedChoiceCol%
                                    Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan on white
                                    Print highlightedChoice$
                                End If
                                ' Highlight the new choice
                                highlightedChoiceRow% = mouseRow%
                                highlightedChoiceCol% = possibleChoicesCol% + choiceLeftPos% - 1
                                Locate highlightedChoiceRow%, highlightedChoiceCol%
                                Color _RGB(0, 255, 255), _RGB(255, 255, 255) ' cyan on white
                                Print choice$
                                highlightedChoice$ = choice$
                            End If
                        End If
                    End If
                End If
                ' Handle no current choice and yet there was a highlighted choice the last time there was a mouse event
                If (choice$ = "") And (highlightedChoice$ <> "") Then
                    ' Remove the highlight from the last highlighted choice
                    Locate highlightedChoiceRow%, highlightedChoiceCol%
                    Color _RGB(0, 139, 139)
                    Print highlightedChoice$
                    highlightedChoice$ = ""
                    highlightedChoiceRow% = 0
                    highlightedChoiceCol% = 0
                End If
            End If
        Else
            If (Asc(c$) >= 32) And (Asc(c$) <= 126) Then
                ' Alphanumeric characters
                If (inp$ = "") And ((c$ = " ") Or (c$ = Chr$(13))) Then
                    ' Ignore the space key or enter key if nothing else has been entered
                Else
                    If Len(inp$) < maxLength Then
                        inp$ = (inp$ + UCase$(c$))
                        Locate currentRow%, currentCol%
                        Color _RGB(0, 0, 0), _RGB(255, 255, 255) ' black on white
                        Print UCase$(c$)
                        Locate currentRow%, currentCol% + 1
                        Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan on white
                        Print cursor$
                        currentCol% = currentCol% + 1
                    Else
                        ' Ignore any input when the string gets too long
                    End If
                End If
            Else
                ' Backspace character
                If c$ = Chr$(8) Then
                    If Len(inp$) > 0 Then
                        inp$ = Left$(inp$, Len(inp$) - 1)
                        Locate currentRow%, currentCol% - 1
                        Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan on white
                        Print cursor$ + " "
                        currentCol% = currentCol% - 1
                    Else
                        ' Ignore any backspace when there is nothing left to delete
                    End If
                Else
                    If enableArrowKeys = 1 Then
                        ' Up arrow
                        If c$ = Chr$(0) + "H" Then
                            If RTrim$(LTrim$(inp$)) = "" Then
                                inp$ = "MOVE AHEAD"
                                c$ = Chr$(13)
                                Locate currentRow%, currentCol%
                                Color _RGB(0, 0, 0), _RGB(255, 255, 255) ' black on white
                                Print inp$
                            End If
                        End If
                        ' Down arrow
                        If c$ = Chr$(0) + "P" Then
                            If RTrim$(LTrim$(inp$)) = "" Then
                                inp$ = "MOVE BACKWARDS"
                                c$ = Chr$(13)
                                Locate currentRow%, currentCol%
                                Color _RGB(0, 0, 0), _RGB(255, 255, 255) ' black on white
                                Print inp$
                            End If
                        End If
                        ' Left arrow
                        If c$ = Chr$(0) + "K" Then
                            If RTrim$(LTrim$(inp$)) = "" Then
                                inp$ = "TURN LEFT"
                                c$ = Chr$(13)
                                Locate currentRow%, currentCol%
                                Color _RGB(0, 0, 0), _RGB(255, 255, 255) ' black on white
                                Print inp$
                            End If
                        End If
                        ' Right arrow
                        If c$ = Chr$(0) + "M" Then
                            If RTrim$(LTrim$(inp$)) = "" Then
                                inp$ = "TURN RIGHT"
                                c$ = Chr$(13)
                                Locate currentRow%, currentCol%
                                Color _RGB(0, 0, 0), _RGB(255, 255, 255) ' black on white
                                Print inp$
                            End If
                        End If
                    End If
                End If
            End If
        End If
    Wend
    ' Clear the current input
    _KeyClear
    ' TODO - If the player uses the mouse to click a choice, blanking the prompt actually blanks the first character of the choice... oops
    ' Clear the cursor
    'Locate currentRow%, currentCol%
    'Print " "
    ' Trim whitespace from the beginning and end of the input string
    GetInput$ = RTrim$(LTrim$(inp$))
    If highlightedChoice$ <> "" Then
        ' Remove the highlight from the last highlighted choice
        Locate highlightedChoiceRow%, highlightedChoiceCol%
        Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan on white
        Print highlightedChoice$
    End If
    ' Reset the foreground text color and print mode of printing text over the book image
    _PrintMode _KeepBackground
    Color _RGB(0, 0, 0)
End Function

' Get the first tag in a string of text
'
' Paramters:
' text - String of text to search
' *tagStart - Starting position of the first tag character (<)
' *tagEnd - Ending position of the last tag character (>)
' *tagValue - If the tag contains a space, the value to the right of the space
'
' Returns:
' First tag
Function GetTag$ (text As String, tagStart As Integer, tagEnd As Integer, tagValue As String)
    GetTag$ = ""
    tagValue = ""
    tagStart = InStr(1, text, "<")
    If tagStart = 0 Then
        tagEnd = 0
        Exit Function
    End If
    tagEnd = InStr(tagStart, text, ">")
    If tagEnd = 0 Then
        Exit Function
    End If
    tagContents$ = Mid$(text, tagStart + 1, tagEnd - tagStart - 1)
    tag$ = LCase$(ListPiece$(tagContents$, " ", 1))
    GetTag$ = tag$
    tagValue = Mid$(tagContents$, Len(tag$) + 2, Len(tagContents$))
End Function

' Increase the player's inventory
'
' Parameters:
' game - Book instance
' item - Item to add to the inventory
Sub IncreaseInventory (game As Book, item As String)
    If Not Contains%(game.inventory, item, ",") Then
        If game.inventory = "" Then
            game.inventory = item
        Else
            game.inventory = game.inventory + "," + item
        End If
        Call ReplaySound(game.inventorySound, 1, 0)
        storyColor& = _DefaultColor
        Color _RGB(255, 140, 0)
        Call PrintPageText(game, "story", CsrLin + 1, "INVENTORY +" + item)
        storyRow% = CsrLin
        Call RefreshControlPage(game, "")
        Color storyColor&
        Locate storyRow%, 1
    End If
End Sub

' Increase the player's score
'
' Parameters:
' game - Book instance
' points - Number of points by which to increase the score
Sub IncreaseScore (game As Book, points As Integer)
    game.score = game.score + points
    game.experiencePoints = game.experiencePoints + points
    Call ReplaySound(game.pointsSound, 1, 0)
    storyColor& = _DefaultColor
    Color _RGB(255, 140, 0)
    Call PrintPageText(game, "story", CsrLin + 1, "SCORE +" + LTrim$(Str$(points)))
    storyRow% = CsrLin
    Call RefreshControlPage(game, "")
    Color storyColor&
    Locate storyRow%, 1
End Sub

' Are two objects touching?
'
' Parameters:
' object1x1 - First object's x1
' object1y1 - First object's y1
' object1x2 - First object's x2
' object1y2 - First object's y2
' object2x1 - Second object's x1
' object2y1 - Second object's y1
' object2x2 - Second object's x2
' object2y2 - Second object's y2
'
' Returns:
' Touching? (1/0)
Function IsOverlap% (object1x1 As Integer, object1y1 As Integer, object1x2 As Integer, object1y2 As Integer, object2x1 As Integer, object2y1 As Integer, object2x2 As Integer, object2y2 As Integer)
    returnVal% = 0
    If ((object1x1 >= object2x1) And (object1x1 <= object2x2)) Or ((object1x2 >= object2x1) And (object1x2 <= object2x2)) Then
        If ((object1y1 >= object2y1) And (object1y1 <= object2y2)) Or ((object1y2 >= object2y1) And (object1y2 <= object2y2)) Then
            returnVal% = 1
        End If
    End If
    If returnVal% = 0 Then
        If ((object2x1 >= object1x1) And (object2x1 <= object1x2)) Or ((object2x2 >= object1x1) And (object2x2 <= object1x2)) Then
            If ((object2y1 >= object1y1) And (object2y1 <= object1y2)) Or ((object2y2 >= object1y1) And (object2y2 <= object1y2)) Then
                returnVal% = 1
            End If
        End If
    End If
    IsOverlap% = returnVal%
End Function

' Listen action
'
' Parameters:
' game - Book instance
Sub ListenAction (game As Book)
    Dim variable As Variable
    Dim currentScene As Scene
    Call WorldScene(RETRIEVE, 1, game.level, game.playerX, game.playerY, currentScene)
    scene$ = currentScene.name

    If game.storySound = 0 Then
        ' TODO - Should also check to see if there is anything implemented for the [[LISTEN]] action for the scene instead of skipping it outright
        Call PrintPageText(game, "story", CsrLin, "You hear nothing.")
    Else
        If _SndPlaying(game.storySound) = 0 Then
            ReplaySound game.storySound, 1, 0
        End If
        ' Execute the LISTEN action
        Call WorldScenePage(RETRIEVE, currentScene.id, 1, pageContents$)
        Call DisplayStoryPage(game, currentScene, pageContents$, "LISTEN", 1)
    End If
    Color _RGB(0, 0, 0)
End Sub

' Add a value to a delimited string
'
' Parameters:
' text - Full string of text
' delimiter - Text that delimits substrings within the full string of text
' value - Delimited value to add to the string
'
' Returns:
' Delimited string with new value
Function ListAdd$ (text As String, delimiter As String, value As String)
    If ListContains%(text, delimiter, value) = 1 Then
        ListAdd$ = text
        Exit Function
    End If
    tempText$ = text
    If Right$(text, 1) <> delimiter Then
        tempText$ = tempText$ + delimiter
    End If
    tempText$ = tempText$ + value
    ListAdd$ = tempText$
End Function

' Is a given value in a delimited string? (1/0)
'
' Parameters:
' text - Full string of text
' delimiter - Text that delimits substrings within the full string of text
' value - Delimited value to find in the string
'
' Returns:
' Contains value? (1/0)
Function ListContains% (text As String, delimiter As String, value As String)
    If InStr(1, delimiter + text + delimiter, delimiter + value + delimiter) > 0 Then
        ListContains% = 1
    Else
        ListContains% = 0
    End If
End Function

' Get the delimited length of a string
'
' Parameters:
' text - Full string of text
' delimiter - Text that delimits substrings within the full string of text
'
' Returns:
' Number of delimited substrings
Function ListLength% (text As String, delimiter As String)
    currentPiece% = 1
    delimiterPos% = InStr(1, text, delimiter)
    If (delimiter = "") Or (delimiterPos% = 0) Then
        ListLength% = currentPiece%
    Else
        position% = 1
        atEnd% = 0
        delimiterLen% = Len(delimiter)
        While atEnd% = 0
            position% = delimiterPos% + delimiterLen%
            delimiterPos% = InStr(position%, text, delimiter)
            currentPiece% = currentPiece% + 1
            If delimiterPos% = 0 Then
                delimiterPos% = Len(text) + 1
                atEnd% = 1
            End If
        Wend
        ListLength% = currentPiece%
    End If
End Function

' Extract a substring for a given delimiter and index position
'
' Parameters:
' text - Full string of text to search
' delimiter - Text that delimits the subpieces in the full string
' index - Delimited position of substring
'
' Returns:
' Delimited substring
Function ListPiece$ (text As String, delimiter As String, index As Integer)
    delimiterPos% = InStr(1, text, delimiter)
    If (delimiter = "") Or (delimiterPos% = 0) Then
        ListPiece$ = text
    Else
        currentPiece% = 1
        position% = 1
        atEnd% = 0
        delimiterLen% = Len(delimiter)
        While (currentPiece% < index) And (atEnd% = 0)
            position% = delimiterPos% + delimiterLen%
            delimiterPos% = InStr(position%, text, delimiter)
            currentPiece% = currentPiece% + 1
            If delimiterPos% = 0 Then
                delimiterPos% = Len(text) + 1
                atEnd% = 1
            End If
        Wend
        ListPiece$ = Mid$(text, position%, delimiterPos% - position%)
    End If
End Function

' Remove a value from a delimited string
'
' Parameters:
' text - Full string of text
' delimiter - Text that delimits substrings within the full string of text
' value - Delimited value to remove from the string
'
' Returns:
' Delimited string without given value
Function ListRemove$ (text As String, delimiter As String, value As String)
    If ListContains%(text, delimiter, value) = 0 Then
        ListRemove$ = text
        Exit Function
    End If
    tempText$ = ""
    startPosition% = InStr(1, delimiter + text + delimiter, delimiter + value + delimiter)
    If startPosition% > 1 Then
        tempText$ = tempText$ + Mid$(text, 1, startPosition% - 1)
    End If
    If (startPosition% + Len(value) - 1) < Len(text) Then
        tempText$ = tempText$ + Mid$(text, startPosition% + Len(value) + 1, Len(text) - (Len(value) + 1))
    End If
    If Right$(tempText$, 1) = delimiter Then
        tempText$ = Mid$(tempText$, 1, Len(tempText$) - 1)
    End If
    ListRemove$ = tempText$
End Function

' Load a new story scene
'
' Parameters:
' game - Book instance
Sub LoadScene (game As Book, newScene As Scene)
    ' Advance the player to the next scene
    Dim lastScene As Scene
    Call WorldScene(RETRIEVE, 1, game.level, game.playerLastX, game.playerLastY, lastScene)
    lastScene$ = lastScene.name

    ' Update the scene's sound
    If lastScene$ <> newScene.name Then
        ' Stop playing the current scene's sound, if any
        If game.storySound <> 0 Then
            StopSound game.storySound
        End If

        ' Start playing the new scene's sound, if any
        game.storySound = PlaySound&(newScene.sound, 1, 1)

        ' Reset the story choices
        game.storyChoices = game.basicStoryChoices
    End If

    ' Refresh the scene's description
    Call LookAction(game)
End Sub

' Load the game story
'
' Parameters:
' game - Book instance
Sub LoadStory (game As Book)
    ' Load the story

    storyFile$ = ""
    file$ = DIR$("*.quest", fileCount%) 'use a file spec ONCE to find the last file name listed

    If fileCount% = 0 Then
        Call PrintPageText(game, "story", CsrLin, "No story files!!!")
    Else
        If fileCount% = 1 Then
            storyFile$ = file$
        Else
            ' There are multiple story files, so build a comma-delimited list of stories
            stories$ = ListPiece$(file$, ".quest", 1) ' Remove the file extension for readability
            maxListLength% = Len(file$)
            Do
                file$ = DIR$("", fileCount%) 'use an empty string parameter to return a list of files!
                If file$ <> "" Then
                    If stories$ <> "" Then
                        stories$ = stories$ + ","
                    End If
                    stories$ = stories$ + ListPiece$(file$, ".quest", 1) ' Remove the file extension for readability
                    If Len(file$) > maxListLength% Then
                        maxListLength% = Len(file$)
                    End If
                End If
            Loop Until Len(file$) = 0 'file list ends with an empty string

            oldBooks& = _LoadImage("old-books.jpg")
            turnPageSound% = 1
            Do
                Call TurnPage(game, turnPageSound%)
                Call DisplayImage(game, "story", oldBooks&)
                Locate CsrLin + 1, 1
                storyFile$ = StoryPrompt$(game, "Choose Quest", stories$, 0, 1, maxListLength%)
                turnPageSound% = 0 ' If the player enters an incorrect choice and the page is turned to reset the screen, be silent
            Loop While Not Contains%(game.storyChoices, storyFile$, ",")
            storyFile$ = storyFile$ + ".quest" ' Re-add the file extension
            _FreeImage oldBooks&
            Locate CsrLin + 3, 1
        End If
    End If

    If _FileExists(storyFile$) Then
        game.storyFile = storyFile$

        Open storyFile$ For Input As #1
        Do Until EOF(1)
            Input #1, fileLine$
            If (Mid$(fileLine$, 1, 1) = "[") And (Mid$(fileLine$, 2, 1) <> "[") Then
                ' New sections are wrapped in brackets, but not double brackets
                section$ = Mid$(fileLine$, 2, Len(fileLine$) - 2)

                ' If a new scene is being introduced, instance it as a proper WorldScene
                If Left$(section$, Len("scene")) = "scene" Then
                    sceneValue$ = ListPiece$(section$, " ", 2)
                    If Left$(sceneValue$, 3) = "id=" Then
                        sceneCoordinates$ = ListPiece$(sceneValue$, "id=", 2)
                        sceneLevel% = Val(ListPiece$(sceneCoordinates$, ",", 1))
                        sceneX% = Val(ListPiece$(sceneCoordinates$, ",", 2))
                        sceneY% = Val(ListPiece$(sceneCoordinates$, ",", 3))
                        Dim scene As Scene
                        Call WorldScene(CREATE, 1, sceneLevel%, sceneX%, sceneY%, scene)
                    End If
                End If
            Else
                If section$ = "title" Then
                    game.story = fileLine$
                    game.title = "Twin Quest: " + game.story
                    _Continue
                End If
                If section$ = "author" Then
                    game.author = fileLine$
                    _Continue
                End If
                If section$ = "credits" Then
                    If game.credits <> "" Then
                        game.credits = game.credits + "<br>"
                    End If
                    game.credits = game.credits + fileLine$
                    _Continue
                End If
                If section$ = "opening music" Then
                    game.openingMusic = fileLine$
                    _Continue
                End If
                If section$ = "high scores" Then
                    If game.highScores <> "" Then
                        game.highScores = game.highScores + "<br>"
                    End If
                    game.highScores = game.highScores + fileLine$
                    _Continue
                End If
                If section$ = "variables" Then
                    name$ = ListPiece$(fileLine$, "=", 1)
                    type$ = "Boolean" ' Only supported variable type at this time
                    value$ = ListPiece$(fileLine$, "=", 2)
                    Dim tempVariable As Variable
                    tempVariable.name = name$
                    tempVariable.type = type$
                    tempVariable.value = value$
                    Call StoryVariable(CREATE, tempVariable)
                End If
                If section$ = "gif" Then
                    game.openingGIF = fileLine$
                    _Continue
                End If
                If Left$(section$, Len("scene")) = "scene" Then
                    Call WorldScene(RETRIEVE, 1, sceneLevel%, sceneX%, sceneY%, scene)

                    sceneValue$ = ListPiece$(section$, " ", 2)

                    ' Scene header
                    If Left$(sceneValue$, 3) = "id=" Then
                        If Left$(fileLine$, 5) = "name=" Then
                            scene.name = ListPiece$(fileLine$, "=", 2)
                            Call WorldScene(UPDATE, 1, sceneLevel%, sceneX%, sceneY%, scene)
                            _Continue
                        End If
                        If Left$(fileLine$, 6) = "color=" Then
                            color$ = ListPiece$(fileLine$, "=", 2)
                            red% = Val(ListPiece$(color$, ",", 1))
                            green% = Val(ListPiece$(color$, ",", 2))
                            blue% = Val(ListPiece$(color$, ",", 3))
                            scene.color = _RGB(red%, green%, blue%)
                            Call WorldScene(UPDATE, 1, sceneLevel%, sceneX%, sceneY%, scene)
                            _Continue
                        End If
                        If Left$(fileLine$, 6) = "sound=" Then
                            sound$ = ListPiece$(fileLine$, "=", 2)
                            scene.sound = sound$
                            Call WorldScene(UPDATE, 1, sceneLevel%, sceneX%, sceneY%, scene)
                            _Continue
                        End If
                        If Left$(fileLine$, 6) = "symbol=" Then
                            symbol$ = ListPiece$(fileLine$, "=", 2)
                            scene.symbol = symbol$
                            Call WorldScene(UPDATE, 1, sceneLevel%, sceneX%, sceneY%, scene)
                            _Continue
                        End If
                        _Continue ' Unsupported scene property
                    End If

                    ' Scene page
                    pageNumber% = Val(sceneValue$)
                    Call WorldScenePage(RETRIEVE, scene.id, pageNumber%, pageContents$)
                    If pageContents$ <> "" Then
                        pageContents$ = pageContents$ + "<div>"
                        pageContents$ = pageContents$ + fileLine$
                        Call WorldScenePage(UPDATE, scene.id, pageNumber%, pageContents$)
                    Else
                        pageContents$ = fileLine$
                        Call WorldScenePage(CREATE, scene.id, pageNumber%, pageContents$)
                    End If
                    _Continue
                End If
            End If
        Loop
        Close #1
    Else
        Call PrintPageText(game, "story", CsrLin, storyFile$ + " does not exist")
    End If

    ' TODO - Game vars should be initialized dynamically by the story file
    game.level = 0
    game.levelSize = 3
    game.doorX = 2
    game.doorY = 0
    game.playerX = game.doorX
    game.playerLastX = game.playerX
    game.playerY = game.doorY
    game.playerLastY = game.playerY
    game.playerDirection = 3
End Sub

' Look action
'
' Parameters:
' game - Book instance
Sub LookAction (game As Book)
    Dim variable As Variable
    Dim scene As Scene
    Call WorldScene(RETRIEVE, 1, game.level, game.playerX, game.playerY, scene)
    playerView$ = scene.name

    ' Execute the LOOK action, followed by displaying the story for the scene
    Call WorldScenePage(RETRIEVE, scene.id, 1, pageContents$)
    Call DisplayStoryPage(game, scene, pageContents$, "LOOK", 1)

    Color _RGB(0, 0, 0) ' Restore the default color
End Sub

' Display the Monster Shooter minigame.
'
' For more 2d gaming fun, see https://www.qb64tutorial.com/lesson19.
'
' Parameters:
' game - Book instance
' backgroundImageFile - File for the image of the background
' storySoundFile - File for the cool boss battle music
' playerImageFile - File for the image of the player
' missileLeftImageFile - File for the image of the missile that starts on the left-hand side of the player
' missileRightImageFile - File for the image of the missile that starts on the right-hand side of the player
' monsterImageFile - File for the image of the monster
' isMonsterVisible - Is the monster visible? (1/0)  If not, the monster's image will not be displayed.
' monsterSoundFile - File for the sound the monster makes
'
' Returns:
' Success? (1/0)
Function MonsterShooter% (game As Book, backgroundImageFile As String, storySoundFile As String, playerImageFile As String, missileLeftImageFile As String, missileRightImageFile As String, monsterImageFile As String, isMonsterVisible As Integer, monsterSoundFile As String)
    'Sub ArcadeShooter (game As Book, playerImageFile As String, playerAttackImageFile As String, monsterImageFile As String, monsterAttackImageFile As String, monsterAttackSoundFile As String)

    ' TODO - Clean up all of this code

    ' TODO - Use a timer to track duration between inbound missile launches.  After X seconds, put an image (cat-missile-left.png and cat-missile-right.png and arrow.png) of the next missile that is ready to go.  Alternate between left and right sides of the player.
    ' Play cat-meowing.mp3 or
    ' TODO - When the user presses the spacebar, a missile will only launch if one is ready.
    ' TODO - (Need to move ready missile with player when they move.  Also need to explode that missile if a fireball hits it.)
    ' TODO - Move the non-static missiles up and shrink them as they ascend.  Handle hitting either a fireball (play zap.mp3) or the bottom of the monster if the monster has static fireballs.

    ' Refresh the screen, blanking out both the story page and the control page
    Call TurnPage(game, 1)
    startingCol% = Pos(0)
    startingRow% = CsrLin
    buffer% = 10 ' x/y coordinate padding to ensure minigame objects remain within the background image

    ' Calculate the text and x/y coordinates of the control page and screen capture it so it can be easily redrawn without redrawing the whole screen
    If game.isRightHanded = 0 Then
        leftColumn% = game.leftPageStartingColumn
    Else
        leftColumn% = game.rightPageStartingColumn
    End If
    rightColumn% = leftColumn% + game.maxPageLength
    leftEdgeX% = (leftColumn% * _FontWidth) - _FontWidth
    rightEdgeX% = rightColumn% * _FontWidth
    topRow% = game.firstRow
    topEdgeY% = (topRow% * _FontHeight) - _FontHeight
    bottomRow% = game.lastRow
    bottomEdgeY% = bottomRow% * _FontHeight
    controlPage& = _NewImage(rightEdgeX% - leftEdgeX%, bottomEdgeY% - topEdgeY%, 32) ' Init the screen capture by matching the size of the control page
    _PutImage (0, 0), 0, controlPage&, (leftEdgeX%, topEdgeY%)-(rightEdgeX%, bottomEdgeY%) ' Copy the exact coordinates from the screen image to the screen capture
    controlX% = leftEdgeX%
    controlY% = topEdgeY%

    ' Display the control page with special instructions
    instructions$ = "To move, hold the LEFT and RIGHT arrow keys.<br>To shoot, press the SPACE key.<br><br>To exit, press the Esc key."
    Call RefreshControlPage(game, instructions$)

    ' Load the image resource files
    backgroundImage& = _LoadImage(backgroundImageFile)
    playerImage& = _LoadImage(playerImageFile)
    monsterImage& = _LoadImage(monsterImageFile)
    missileLeftImage& = _LoadImage(missileLeftImageFile)
    missileRightImage& = _LoadImage(missileLeftImageFile)
    boomImage& = _LoadImage("boom.png")
    explodeImage& = _LoadImage("explode.png")

    ' Load the audio resource files
    storySound& = PlaySound&(storySoundFile, 1, 1) ' Load and play cool boss battle music
    monsterSound& = PlaySound&(monsterSoundFile, 1, 0) ' Load and play monster's vocal challenge to the player
    pauseSound& = _SndOpen("pause.mp3") ' Load pause-the-game sound
    _SndVol pauseSound&, 1
    zapSound& = _SndOpen("zap.mp3") ' Load the fireball-missile collision sound
    _SndVol zapSound&, 1

    ' Display the story page and screen capture it so it can be easily redrawn without redrawing the whole screen
    Locate startingRow%, startingCol%
    Call DisplayImage(game, "story", backgroundImage&)
    leftEdgeX% = game.lastImageX
    rightEdgeX% = game.lastImageX + _Width(backgroundImage&)
    topEdgeY% = game.lastImageY
    bottomEdgeY% = game.lastImageY + _Height(backgroundImage&)
    startingScreen& = _NewImage(rightEdgeX% - leftEdgeX%, bottomEdgeY% - topEdgeY%, 32) ' Init the screen capture by matching the size of the game area
    _PutImage (0, 0), 0, startingScreen&, (leftEdgeX%, topEdgeY%)-(rightEdgeX%, bottomEdgeY%) ' Copy the exact coordinates from the screen image to the screen capture

    ' Initialize the player's starting position
    playerSizeX% = _Width(monsterImage&)
    playerSizeY% = playerSizeX% * (_Height(playerImage&) / _Width(playerImage&))
    playerX% = (leftEdgeX% + rightEdgeX%) / 2
    playerY% = bottomEdgeY% - playerSizeY% - 25 ' Buffer of 25 ensures player is inside of background image

    ' Initialize the monster's starting position, movement, and health
    monsterX% = (leftEdgeX% + rightEdgeX%) / 2
    monsterY% = (startingRow% * _FontHeight) + buffer% ' Buffer ensures monster is inside of background image
    monsterWidth% = _Width(monsterImage&)
    monsterHeight% = _Height(monsterImage&)
    monsterDirection% = 1 ' 1 = Move to the right, -1 = Move to the left
    Randomize Timer
    randomNumber% = Int(Rnd * 400) + 1
    monsterDestinationX% = monsterX% + (randomNumber% * monsterDirection%)
    If monsterDestinationX% < leftEdgeX% Then
        monsterDestinationX% = leftEdgeX%
    End If
    If monsterDestinationX% > (rightEdgeX% - _Width(monsterImage&)) Then
        monsterDestinationX% = (rightEdgeX% - _Width(monsterImage&))
    End If
    halfMonsterDestinationX% = monsterX% + ((randomNumber% / 2) * monsterDirection%)
    monsterHalfwayThere% = 0
    monsterHP% = 100

    ' Initialize the fireball templates
    multiplier! = .125 ' 1/8
    Dim fireballTemplate(4) As Sprite
    fireballTemplate(1).direction = -1
    fireballTemplate(1).name = "hot"
    fireballTemplate(1).image = _LoadImage("fireball1.png")
    fireballTemplate(1).size = _Width(monsterImage&) * multiplier!
    fireballTemplate(1).speed = 4
    fireballTemplate(1).y = monsterY% + (_Height(monsterImage&) * .25)
    fireballTemplate(2).direction = -1
    fireballTemplate(2).name = "blazin"
    fireballTemplate(2).image = _LoadImage("fireball3.png")
    fireballTemplate(2).size = _Width(monsterImage&) * multiplier!
    fireballTemplate(2).speed = 3
    fireballTemplate(2).y = monsterY% + (_Height(monsterImage&) * .5)
    fireballTemplate(3).direction = 1
    fireballTemplate(3).name = "inferno"
    fireballTemplate(3).image = _LoadImage("fireball4.png")
    fireballTemplate(3).size = _Width(monsterImage&) * multiplier!
    fireballTemplate(3).speed = 3
    fireballTemplate(3).y = monsterY% + (_Height(monsterImage&) * .5)
    fireballTemplate(4).direction = 1
    fireballTemplate(4).name = "hotter"
    fireballTemplate(4).image = _LoadImage("fireball2.png")
    fireballTemplate(4).size = _Width(monsterImage&) * multiplier!
    fireballTemplate(4).speed = 4
    fireballTemplate(4).y = monsterY% + (_Height(monsterImage&) * .25)
    If game.difficulty = HARD Then
        growthRate! = .4
    Else
        growthRate! = .2
    End If

    ' Initialize the fireballs
    Dim fireball(50) As Sprite
    fireballIndex% = 0
    fireballCount% = 0
    fireballsSpawned% = 0
    fireballsUnleashed% = 0

    ' Initialize the missile templates
    multiplier! = .25 ' 1/4
    Dim missileTemplate(4) As Sprite
    missileTemplate(1).name = "normal"
    missileTemplate(1).image = _LoadImage(missileLeftImageFile)
    missileTemplate(1).size = playerSizeY% * .75 ' 3/4
    missileTemplate(1).speed = 4
    missileTemplate(1).y = playerY%
    missileTemplate(2).name = "normal"
    missileTemplate(2).image = _LoadImage(missileRightImageFile)
    missileTemplate(2).size = playerSizeY% * .75 ' 3/4
    missileTemplate(2).speed = 4
    missileTemplate(2).y = playerY%
    missileTemplate(3).name = "whopper"
    missileTemplate(3).image = _LoadImage(missileLeftImageFile)
    missileTemplate(3).size = playerSizeY%
    missileTemplate(3).speed = 3
    missileTemplate(3).y = playerY%
    missileTemplate(4).name = "whopper"
    missileTemplate(4).image = _LoadImage(missileRightImageFile)
    missileTemplate(4).size = playerSizeY%
    missileTemplate(4).speed = 3
    missileTemplate(4).y = playerY%

    ' Initialize the missiles
    Dim missile(50) As Sprite
    missileIndex% = 0
    missileCount% = 0
    missileSpawned% = 0
    nextMissileSpawn! = Timer + 1
    If nextMissileSpawn! > 86399 Then
        nextMissileSpawn! = 1 ' Handle going past midnight
    End If
    nextMissileDirection% = -1
    missileWidthRatio! = _Width(missileLeftImage&) / _Height(missileLeftImage&)
    whopperCounter% = 0

    Do
        If game.difficulty = HARD Then
            _Limit 50 ' 50 frames per second
        Else
            _Limit 35 ' 35 frames per second
        End If

        ' Display the background, player, and monster
        _PutImage (leftEdgeX%, topEdgeY%), startingScreen&, 0
        _PutImage (playerX%, playerY%)-(playerX% + playerSizeX%, playerY% + playerSizeY%), playerImage&
        If isMonsterVisible = 1 Then
            _PutImage (monsterX%, monsterY%), monsterImage&
        End If

        ' Display each fireball
        If fireballIndex% >= 1 Then
            For i% = 1 To fireballCount%
                If fireball(i%).visible = 1 Then
                    _PutImage (fireball(i%).x, fireball(i%).y)-(fireball(i%).x + fireball(i%).size, fireball(i%).y + fireball(i%).size), fireball(i%).image
                End If
            Next i%
        End If

        ' Display each missile
        If missileIndex% >= 1 Then
            For i% = 1 To missileCount%
                If missile(i%).visible = 1 Then
                    _PutImage (missile(i%).x, missile(i%).y)-(missile(i%).x + (missile(i%).size * missileWidthRatio!), missile(i%).y + missile(i%).size), missile(i%).image
                End If
            Next i%
        End If

        ' Move the monster
        spawnFireballs% = 0
        xDelta% = 2 * monsterDirection%
        monsterX% = monsterX% + xDelta%

        ' Difficulty Levels:
        ' EASY... If the monster is halfway to its destination, toggle between spawning new fireballs and unleashing the current fireballs
        ' HARD... If the monster is halfway to its destination, unleash the current fireballs.  And spawn new fireballs when it reaches its destination.

        ' Evaluate whether the monster is now halfway to its destination
        If (monsterHalfwayThere% = 0) And (((monsterDirection% = 1) And (monsterX% >= halfMonsterDestinationX%)) Or ((monsterDirection% = -1) And (monsterX% <= halfMonsterDestinationX%))) Then
            If fireballsSpawned% = 0 Then
                spawnFireballs% = 1 ' Request new fireballs
            Else
                ' Fireballs attached to monster are now free to descend
                For i% = 1 To fireballCount%
                    If fireball(i%).action1 = "static" Then
                        fireball(i%).action1 = "unleashed"
                    End If
                Next i%
                fireballsSpawned% = 0
            End If
            Call ReplaySound(monsterSound&, 1, 0)
            monsterHalfwayThere% = 1
        End If

        ' Evaluate whether the monster is at its destination
        If ((monsterDirection% = 1) And (monsterX% >= monsterDestinationX%)) Or ((monsterDirection% = -1) And (monsterX% <= monsterDestinationX%)) Then
            monsterDirection% = monsterDirection% * -1
            ' Randomly compute number of iterations until next change (will need to compute edge in that direction for going too far)
            randomNumber% = Int(Rnd * 400) + 1
            monsterDestinationX% = monsterX% + (randomNumber% * monsterDirection%)
            If monsterDestinationX% < leftEdgeX% Then
                monsterDestinationX% = leftEdgeX%
            End If
            If monsterDestinationX% > (rightEdgeX% - _Width(monsterImage&)) Then
                monsterDestinationX% = (rightEdgeX% - _Width(monsterImage&))
            End If
            ' Compute halfway to the destination
            halfMonsterDestinationX% = (monsterDestinationX% + monsterX%) / 2
            monsterHalfwayThere% = 0
            ' For HARD difficulty, always spawn new fireballs now
            If game.difficulty = HARD Then
                spawnFireballs% = 1 ' Request new fireballs
            End If
        End If

        ' Move each fireball
        If fireballIndex% >= 1 Then
            For i% = 1 To fireballCount%
                If fireball(i%).visible = 1 Then
                    If fireball(i%).action1 = "static" Then
                        ' New fireballs stay with the monster
                        fireball(i%).x = fireball(i%).x + xDelta%
                    Else
                        ' Unleashed fireballs descend and move slightly left or right depending on their starting position relative to the monster
                        fireball(i%).x = fireball(i%).x + fireball(i%).direction
                        fireball(i%).y = fireball(i%).y + fireball(i%).speed
                        ' TODO - Rotate the fireball as it descends (https://www.qb64tutorial.com/lesson17)
                        fireball(i%).size = fireball(i%).size + growthRate!
                        If (fireball(i%).x < leftEdgeX%) Or ((fireball(i%).x + fireball(i%).size) > rightEdgeX%) Or ((fireball(i%).y + fireball(i%).size) > bottomEdgeY%) Then
                            fireball(i%).visible = 0
                        End If
                    End If
                End If
            Next i%
        End If

        ' Spawn new fireballs
        If spawnFireballs% = 1 Then
            If fireballIndex% > 46 Then
                fireballIndex% = 0
            End If
            For i% = 1 To 4
                fireballIndex% = fireballIndex% + 1
                fireball(fireballIndex%).action1 = "static"
                fireball(fireballIndex%).direction = fireballTemplate(i%).direction
                fireball(fireballIndex%).name = fireballTemplate(i%).name
                fireball(fireballIndex%).image = fireballTemplate(i%).image
                fireball(fireballIndex%).size = fireballTemplate(i%).size
                fireball(fireballIndex%).speed = fireballTemplate(i%).speed
                If i% = 1 Then
                    fireball(fireballIndex%).x = monsterX%
                End If
                If i% = 2 Then
                    fireball(fireballIndex%).x = monsterX% + (_Width(monsterImage&) * .25) - 1
                End If
                If i% = 3 Then
                    fireball(fireballIndex%).x = monsterX% + (_Width(monsterImage&) * .75) - fireballTemplate(3).size - 1
                End If
                If i% = 4 Then
                    fireball(fireballIndex%).x = monsterX% + (_Width(monsterImage&)) - fireballTemplate(4).size - 1
                End If
                fireball(fireballIndex%).y = fireballTemplate(i%).y
                fireball(fireballIndex%).visible = 1
            Next i%
            If fireballCount% < fireballIndex% Then
                fireballCount% = fireballIndex%
            End If
            spawnFireballs% = 0
            fireballsSpawned% = 1
        End If

        ' TODO - Move each missile

        ' Move player based on input
        oldPlayerX% = playerX%
        keyHit% = _KeyHit
        If _KeyDown(19200) Then
            ' Move player left
            If playerX% > (leftEdgeX% + buffer% + 4) Then
                playerX% = playerX% - 4
            End If
        End If
        If _KeyDown(19712) Then
            ' Move player right
            If (playerX% + playerSizeX%) < (rightEdgeX% - buffer% - 4) Then
                playerX% = playerX% + 4
            End If
        End If
        If keyHit% = 32 Then
            ' Spacebar... fire missile
            If missileIndex% > 0 Then
                If missile(missileIndex%).action1 = "static" Then
                    missile(missileIndex%).action1 = "unleashed"
                    ' Calculate the next missile
                    nextMissileSpawn! = Timer + 1
                    If nextMissileSpawn! > 86399 Then
                        nextMissileSpawn! = 1 ' Handle going past midnight
                    End If
                    nextMissileDirection% = nextMissileDirection% * -1 ' Toggle between being a left-hand missile and a right-hand missile
                    whopperCounter% = whopperCounter% + 1
                    If whopperCounter% >= 3 Then
                        whopperCounter% = 0
                    End If
                    missileSpawned% = 0
                End If
            End If
        End If
        playerXDelta% = playerX% - oldPlayerX%

        ' Move each visible missile
        If missileIndex% >= 1 Then
            For i% = 1 To missileCount%
                If missile(i%).visible = 1 Then
                    If missile(i%).action1 = "static" Then
                        ' New missiles move the same direction and amount as the player
                        missile(i%).x = missile(i%).x + playerXDelta%
                    Else
                        ' Unleashed missiles ascend
                        missile(i%).y = missile(i%).y - fireball(i%).speed
                        missile(i%).size = missile(i%).size - growthRate!
                        If missile(i%).y < topEdgeY% Then
                            missile(i%).visible = 0
                        End If
                    End If
                End If
            Next i%
        End If

        ' For each visible fireball, compute whether the fireball is now touching either a missile or the player (and show explode or BOOMM)
        playerX2% = playerX% + playerSizeX%
        playerMiddleX% = (playerX% + playerX2%) / 2
        playerY2% = playerY% + playerSizeY%
        playerMiddleY% = (playerY% + playerY2%) / 2
        If fireballIndex% >= 1 Then
            For i% = 1 To fireballCount%
                If fireball(i%).visible = 1 Then
                    ' Calculate the center of the fireball
                    fireballMiddleX% = (fireball(i%).x + fireball(i%).x + fireball(i%).size) / 2
                    fireballMiddleY% = (fireball(i%).y + fireball(i%).y + fireball(i%).size) / 2
                    halfFireballWidth% = fireball(i%).size / 2

                    isCollision% = 0

                    ' Is the fireball touching a missile?
                    If missileIndex% >= 1 Then
                        For j% = 1 To missileCount%
                            If missile(j%).visible = 1 Then
                                If IsOverlap%(missile(j%).x, missile(j%).y, missile(j%).x + (missile(j%).size * missileWidthRatio!), missile(j%).y + missile(j%).size, fireball(i%).x, fireball(i%).y, fireball(i%).x + fireball(i%).size, fireball(i%).y + fireball(i%).size) Then
                                    isCollision% = 1
                                    collisionImage& = explodeImage&
                                    objectMiddleX% = (missile(j%).x + missile(j%).x + (missile(j%).size * missileWidthRatio!)) / 2
                                    objectMiddleY% = (missile(j%).y + missile(j%).y + missile(j%).size) / 2
                                    fireball(i%).action2 = "hit missile"
                                    fireball(i%).action3 = "exploding"
                                    missile(j%).action2 = "hit fireball"
                                    missile(j%).action3 = "burning"
                                    Call ReplaySound(zapSound&, 1, 0)
                                    Exit For
                                End If
                            End If
                        Next j%
                    End If

                    ' Is the fireball touching the player?
                    If (isCollision% = 0) And (IsOverlap%(playerX%, playerY%, playerX2%, playerY2%, fireball(i%).x, fireball(i%).y, fireball(i%).x + fireball(i%).size, fireball(i%).y + fireball(i%).size)) Then
                        If fireball(i%).action2 <> "hit player" Then
                            ' Decrease hit points, but only once per fireball
                            fireball(i%).action2 = "hit player"
                            fireballDamage% = 0
                            If fireball(i%).name = "hot" Then
                                fireballDamage% = 10
                            Else
                                If fireball(i%).name = "hotter" Then
                                    fireballDamage% = 15
                                Else
                                    If fireball(i%).name = "blazin" Then
                                        fireballDamage% = 20
                                    Else
                                        If fireball(i%).name = "inferno" Then
                                            fireballDamage% = 25
                                        End If
                                    End If
                                End If
                            End If
                            Call DecreaseHitPoints(game, fireballDamage%, 0)
                            ' Redisplay the control page to refresh the player's hit points
                            _PutImage (controlX%, controlY%), controlPage&, 0
                            Call RefreshControlPage(game, instructions$)
                        End If
                        isCollision% = 1
                        collisionImage& = boomImage&
                        objectMiddleX% = playerMiddleX%
                        objectMiddleY% = playerMiddleY%
                    End If

                    If isCollision% = 1 Then
                        ' Center the collision between the two objects and display it
                        collisionCenterX% = (objectMiddleX% + fireballMiddleX%) / 2
                        collisionCenterY% = (objectMiddleY% + fireballMiddleY%) / 2
                        collisionLeftX% = collisionCenterX% - halfFireballWidth%
                        collisionRightX% = collisionCenterX% + halfFireballWidth%
                        collisionTopY% = collisionCenterY% - halfFireballWidth% ' Fireballs are circular so using the width here is okay
                        collisionBottomY% = collisionCenterY% + halfFireballWidth% ' Fireballs are circular so using the width here is okay
                        _PutImage (collisionLeftX%, collisionTopY%)-(collisionRightX%, collisionBottomY%), collisionImage&
                    End If
                End If
            Next i%
        End If

        ' Destroy any fireballs that were hit by a missile but are no longer exploding
        If fireballIndex% >= 1 Then
            For i% = 1 To fireballCount%
                If fireball(i%).visible = 1 Then
                    If fireball(i%).action2 = "hit missile" Then
                        If fireball(i%).action3 = "exploding" Then
                            fireball(i%).action3 = ""
                        Else
                            fireball(i%).visible = 0
                        End If
                    End If
                End If
            Next i%
        End If

        ' Destroy any missiles that were hit by a fireball but are no longer burning
        If missileIndex% >= 1 Then
            For i% = 1 To missileCount%
                If missile(i%).visible = 1 Then
                    If missile(i%).action2 = "hit fireball" Then
                        If missile(i%).action3 = "burning" Then
                            missile(i%).action3 = ""
                        Else
                            missile(i%).visible = 0
                        End If
                    End If
                End If
            Next i%
        End If

        ' For each visible missile, compute whether the missile is now touching the monster (and show BOOMM)
        monsterX2% = monsterX% + monsterWidth%
        monsterMiddleX% = (monsterX% + monsterX2%) / 2
        monsterY2% = monsterY% + monsterHeight%
        monsterMiddleY% = (monsterY% + monsterY2%) / 2
        If missileIndex% >= 1 Then
            For i% = 1 To missileCount%
                If missile(i%).visible = 1 Then
                    ' Calculate the center of the missile
                    If IsOverlap%(missile(i%).x, missile(i%).y, missile(i%).x + (missile(i%).size * missileWidthRatio!), missile(i%).y + missile(i%).size, monsterX%, monsterY%, monsterX2%, monsterY2%) Then
                        If missile(i%).action2 = "" Then
                            missile(i%).action2 = "hit monster"
                            If missile(i%).name = "whopper" Then
                                Call ReplaySound(game.heavyHitSound, 1, 0)
                                monsterHP% = monsterHP% - 20
                            Else
                                Call ReplaySound(game.hitSound, 1, 0)
                                monsterHP% = monsterHP% - 10
                            End If
                        End If
                        missileMiddleX% = (missile(i%).x + missile(i%).x + (missile(i%).size * missileWidthRatio!)) / 2
                        missileMiddleY% = (missile(i%).y + missile(i%).y + missile(i%).size) / 2

                        ' Center the collision between the two objects and display it
                        halfMissileSize% = missile(i%).size / 2
                        collisionCenterX% = (missileMiddleX% + monsterMiddleX%) / 2
                        collisionCenterY% = (missileMiddleY% + monsterMiddleY%) / 2
                        collisionLeftX% = collisionCenterX% - halfMissileSize%
                        collisionRightX% = collisionCenterX% + halfMissileSize%
                        collisionTopY% = collisionCenterY% - halfMissileSize%
                        collisionBottomY% = collisionCenterY% + halfMissileSize%
                        _PutImage (collisionLeftX%, collisionTopY%)-(collisionRightX%, collisionBottomY%), boomImage&
                    End If
                End If
            Next i%
        End If

        ' Spawn a new missile
        If (missileSpawned% = 0) And (Timer >= nextMissileSpawn!) Then
            If missileIndex% >= 50 Then
                missileIndex% = 0
            End If

            If nextMissileDirection% = -1 Then
                template% = 1
            Else
                template% = 2
            End If
            If whopperCounter% = 2 Then
                template% = template% + 2
            End If

            missileIndex% = missileIndex% + 1
            missile(missileIndex%).action1 = "static"
            missile(missileIndex%).name = missileTemplate(template%).name
            missile(missileIndex%).image = missileTemplate(template%).image
            missile(missileIndex%).size = missileTemplate(template%).size
            missile(missileIndex%).speed = missileTemplate(template%).speed
            If (template% = 1) Or (template% = 3) Then
                missile(missileIndex%).x = playerX% ' Left-hand side missile
            End If
            If (template% = 2) Or (template% = 4) Then
                missile(missileIndex%).x = playerX% + playerSizeX% - (missile(missileIndex%).size * missileWidthRatio!) - 1 ' Right-hand side missile
            End If
            missile(missileIndex%).y = missileTemplate(template%).y
            missile(missileIndex%).visible = 1

            If missileCount% < missileIndex% Then
                missileCount% = missileIndex%
            End If

            missileSpawned% = 1
        End If

        ' TODO - For each visible missile, compute whether the missile is touching the monster (and show boom)
        ' TODO - When NIGHT OWL has his fireballs loaded and a missile reaches his bottom y coordinate, show a shield and play a soft vaporization sound

        _Display ' Update the screen with changes

        ' "P" or "p" was pressed so pause the game
        If (keyHit% = 80) Or (keyHit% = 112) Then
            ' Silence all game sounds
            _SndPause storySound&
            _SndPause monsterSound&

            ' Play the pause sound
            _SndPlay pauseSound&

            ' Suspend input until the player press the any key
            Sleep
            _KeyClear

            ' Continue game sounds
            _SndPlay storySound&
            _SndPlay monsterSound&
        End If
        ' "X" or "x" was pressed so win the game
        If (keyHit% = 88) Or (keyHit% = 120) Then
            monsterHP% = 0
        End If
    Loop Until (_KeyDown(27)) Or (game.hitPoints = 0) Or (monsterHP% <= 0)

    _AutoDisplay ' Resume automatically updating the screen with changes rather than controlling the number of frames per second

    ' Refresh the pages
    '_PutImage (leftEdgeX%, topEdgeY%), startingScreen&, 0
    ' Redisplay the control page to refresh the player's hit points
    '_PutImage (controlX%, controlY%), controlPage&, 0
    'Call RefreshControlPage(game, "")

    ' Free up the image resources
    _FreeImage startingScreen&
    _FreeImage controlPage&
    _FreeImage backgroundImage&
    _FreeImage playerImage&
    _FreeImage monsterImage&
    _FreeImage missileLeftImage&
    _FreeImage missileRightImage&
    _FreeImage boomImage&
    _FreeImage explodeImage&
    For i% = 1 To 4
        _FreeImage fireballTemplate(i%).image
    Next i%

    ' Silence all game sounds and free up the sound resources
    Call StopSound(monsterSound&)
    Call StopSound(storySound&)
    _SndClose pauseSound&

    ' Reset the text cursor
    Locate startingRow%, startingCol%

    ' Victory is determined by the player having hit points and the monster having none
    If (game.hitPoints > 0) And (monsterHP% <= 0) Then
        Call ReplaySound(game.victorySound, 1, 0)
        MonsterShooter% = 1
    Else
        Call ReplaySound(game.deathHitSound, 1, 0)
        MonsterShooter% = 0
    End If
End Function

' Move Ahead action
'
' Parameters:
' game - Book instance
Sub MoveAheadAction (game As Book)
    Dim variable As Variable
    aheadX% = game.playerX
    aheadY% = game.playerY
    If game.playerDirection = game.directions.north Then
        aheadY% = aheadY% - 1
    End If
    If game.playerDirection = game.directions.east Then
        aheadX% = aheadX% + 1
    End If
    If game.playerDirection = game.directions.south Then
        aheadY% = aheadY% + 1
    End If
    If game.playerDirection = game.directions.west Then
        aheadX% = aheadX% - 1
    End If

    Dim newScene As Scene
    Call WorldScene(RETRIEVE, 1, game.level, aheadX%, aheadY%, newScene)
    scene$ = newScene.name

    If scene$ = "" Then
        Call PrintPageText(game, "story", CsrLin, "There is nothing ahead.  You are facing the edge of the world.")
        Print
        Call PrintPageText(game, "story", CsrLin, "Jumping off the edge of the world is NOT allowed in this game.")
    Else
        game.playerLastX = game.playerX
        game.playerX = aheadX%
        game.playerLastY = game.playerY
        game.playerY = aheadY%
        Call LoadScene(game, newScene)
    End If
End Sub

' Move Backwards action
'
' Parameters:
' game - Book instance
Sub MoveBackwardsAction (game As Book)
    Call PrintPageText(game, "story", CsrLin, "Please don't try walking backwards.  You might trip.")
End Sub

' Play a sound file
'
' Parameters:
' file - File location (WAV, OGG, or MP3 file types)
' volume - 0 to 1 value for volume (0=no sound, 1=max)
' loopSound - Play the sound in a continuous loop? (1/0)
'
' Returns:
' soundHandle&
Function PlaySound& (file As String, volume As Single, loopSound As Integer)
    soundHandle& = _SndOpen(file)
    ' TODO - ReplaySound() should be called
    If soundHandle& = 0 Then
        Beep
    Else
        _SndVol soundHandle&, volume
        _SndPlay soundHandle&
        If loopSound Then
            _SndLoop soundHandle&
        End If
    End If
    PlaySound& = soundHandle&
End Function

' Prompt the user to press the any key in order to turn the page
'
' Parameters:
' game - Book instance
' isPageBreak - Is this intended to interrupt (meaning, not end) a page? (1/0)
Sub PressAnyKey (game As Book, isPageBreak As Integer)
    ' Remember the story color
    storyColor& = _DefaultColor
    ' Display the prompt text
    Color _RGB(0, 139, 139) ' dark cyan
    curRow% = CsrLin
    ' TODO - If just a pause before flipping the page, could add forward and backwards buttons to the bottom of the story page to more effectively communicate the next step for the user
    If isPageBreak <> 0 Then
        prompt$ = "Press any key to continue"
    Else
        prompt$ = "Press any key to turn the page -->"
    End If
    promptLength% = Len(prompt$)

    If isPageBreak <> 0 Then
        Call PrintPageText(game, "STORY", curRow% + 1, prompt$)
        x1% = (game.cursor.lastColumn - 1) * _FontWidth
        x2% = x1% + (promptLength% * _FontWidth)
        y1% = (curRow%) * _FontHeight
        y2% = y1% + (_FontHeight)
        background& = _NewImage(x2% - x1%, y2% - y1%, 32) ' Init the text cover by matching the size of the printed text area
        _PutImage (0, 0), game.image, background&, (x1%, y1%)-(x2%, y2%) ' Copy the exact coordinates from the book image to the text cover
    Else
        Call PrintPageText(game, "STORY", game.lastRow - 1, prompt$)
    End If
    ' TODO - Refactor this so the mouse can be used to click the text
    ' Suspend program execution until the any key is pressed
    _KeyClear
    Sleep
    _KeyClear
    If isPageBreak <> 0 Then
        _PutImage (x1%, y1%), background&, 0 ' Copy the full contents of the text cover to the screen using the exact coordinates of the printed text area
        _FreeImage background&
    End If
    ' Reposition the cursor
    Color storyColor& ' Restore the story color
    Locate curRow%, 1
End Sub

' Print text on a designated book page
'
' Parameters:
' game - Book instance
' page - (STORY/CONTROL) Which page of the book to use
' row - Text row
' text - Text to print
Sub PrintPageText (game As Book, page As String, row As Integer, text As String)
    ' Support the <br> tag by calling this sub once for each <br>-delimited section of text
    breakLines% = ListLength%(text, "<br>") - 1
    If breakLines% > 0 Then
        For i = 1 To breakLines% + 1
            breakLine$ = ListPiece$(text, "<br>", i)
            Call PrintPageText(game, page, row + i - 1, breakLine$)
        Next i
        Exit Sub
    End If

    lastColumn = 0

    ' Determine the starting position for the text based on the designated page
    page = UCase$(page)
    If ((page = "STORY") And (game.isRightHanded = 1)) Or ((page = "CONTROL") And (game.isRightHanded = 0)) Then
        If (text = "Press any key to continue") Or (text = "Press any key to turn the page -->") Then
            ' TODO - The text should really be something like a constant
            column% = (game.leftPageStartingColumn + game.maxPageLength) - Len(text) - 4
        Else
            column% = game.leftPageStartingColumn
        End If
    Else
        If ((page = "STORY") And (game.isRightHanded = 0)) Or ((page = "CONTROL") And (game.isRightHanded = 1)) Then
            If (row = game.firstRow) Or (text = "Press any key to continue") Then
                ' By convention, right-align text on the first row of the right-hand page
                column% = (game.rightPageStartingColumn + game.maxPageLength) - Len(text)
            Else
                column% = game.rightPageStartingColumn
            End If
        Else
            If 1 / 0 Then
                ' Let the programmer know they passed something... sinister
            End If
        End If
    End If

    ' Wrap the text to make it fit the pages.
    startingTextLength = 0
    Do
        Locate row, column%
        game.cursor.lastColumn = column%
        textLength = Len(text)
        If startingTextLength = 0 Then
            startingTextLength = textLength
        End If
        If textLength > game.maxPageLength Then
            ' Word wrap
            firstSpacePosition = _InStrRev(game.maxPageLength, text, " ")
            If firstSpacePosition > 0 Then
                ' Display all characters up to the first space at or to the left of position 66
                Print Left$(text, firstSpacePosition)
                text = LTrim$(Mid$(text, firstSpacePosition + 1, textLength))
            Else
                ' Display as many characters of the text that can fit on the line plus a hyphen
                Print Left$(text, game.maxPageLength - 1) + "-"
                text = LTrim$(Mid$(text, game.maxPageLength, textLength))
            End If
        Else
            ' All text can fit on the current line
            If startingTextLength = textLength Then
                ' Allow the text to begin with one or more spaces if this is the first (non-wrapped) text line
                Print text
            Else
                Print LTrim$(text)
            End If
            text = ""
        End If
        row = row + 1
    Loop Until text = ""

    If page = "STORY" Then
        game.cursor.lastStoryRow = CsrLin
    Else
        game.cursor.lastControlRow = CsrLin
    End If
End Sub

' Refresh the control page
'
' Parameters:
' game - Book instance
' [instructions] - Optional.  Special game instructions to display instead of the default game actions.
Sub RefreshControlPage (game As Book, instructions As String)
    Color _RGB(146, 146, 146) ' darker grey
    firstRow% = game.firstRow + 2
    If game.playerMode = "" Then
        Call PrintPageText(game, "control", firstRow%, "_" + game.player + String$(62 - Len(game.player), "_"))
    Else
        playerMode$ = " (" + game.playerMode + ")"
        playerModeLength% = Len(playerMode$)
        Call PrintPageText(game, "control", firstRow%, "_" + game.player + playerMode$ + String$(62 - Len(game.player) - playerModeLength%, "_"))
    End If
    Color _RGB(0, 0, 0) ' black
    Call PrintPageText(game, "control", CsrLin, "Score: " + LTrim$(Str$(game.score)))
    Call PrintPageText(game, "control", CsrLin, "Inventory: " + game.inventory)
    Print
    Call PrintPageText(game, "control", CsrLin, "HP:" + Str$(game.hitPoints))
    Call PrintPageText(game, "control", CsrLin, "MP:" + Str$(game.magicPoints))
    Call PrintPageText(game, "control", CsrLin, "XP:" + Str$(game.experiencePoints))
    Color _RGB(146, 146, 146) ' darker grey
    Call PrintPageText(game, "control", CsrLin, "_______________________________________________________________")
    Color _RGB(0, 0, 0) ' black
    Print

    ' TODO - Refactor this out
    Dim playerSymbol(4) As String
    playerSymbol(1) = "^"
    playerSymbol(2) = ">"
    playerSymbol(3) = "v"
    playerSymbol(4) = "<"

    If instructions <> "" Then
        Print
        Call PrintPageText(game, "control", CsrLin, instructions)
        Exit Sub
    End If

    If game.hitPoints <= 0 Then
        Exit Sub
    End If

    ' Display the map
    Dim mapInfo(5) As String ' TODO - Refactor this out
    directionWord$ = ""
    If game.playerDirection = game.directions.north Then
        directionWord$ = "North"
    End If
    If game.playerDirection = game.directions.east Then
        directionWord$ = "East"
    End If
    If game.playerDirection = game.directions.south Then
        directionWord$ = "South"
    End If
    If game.playerDirection = game.directions.west Then
        directionWord$ = "West"
    End If
    For i% = 1 To 5 Step 1
        mapInfo(i%) = ""
    Next
    If (game.playerX > 0) And (game.playerY > 0) And (game.isOver = 0) Then
        mapInfo(1) = "Level: " + LTrim$(Str$(game.level))
        mapInfo(2) = "Location: " + LTrim$(Str$(game.playerX)) + "," + LTrim$(Str$(game.playerY))
        mapInfo(3) = "Facing: " + directionWord$
    End If
    Call PrintPageText(game, "control", CsrLin, String$((game.levelSize - 1) / 2, " ") + "Map")
    curRow% = CsrLin
    Call PrintPageText(game, "control", CsrLin, String$(game.levelSize + 2, "-")) ' Top border

    Dim scene As Scene
    For y% = 1 To game.levelSize
        'Call PrintPageText(game, "control", CsrLin, "|" + String$(game.levelSize, " ") + "|" + "   " + mapInfo(y%))
        Call PrintPageText(game, "control", CsrLin, "|")
        If game.level > 0 Then
            _PrintMode _FillBackground
            For x% = 1 To game.levelSize
                Call WorldScene(RETRIEVE, 1, game.level, x%, y%, scene)
                Color , scene.color
                Locate CsrLin - 1, game.cursor.lastColumn + x%
                Print " "
            Next x%
            _PrintMode _KeepBackground
        End If
        Locate CsrLin - 1, game.cursor.lastColumn + game.levelSize + 1
        Color _RGB(0, 0, 0)
        Print "|" + "   " + mapInfo(y%)
    Next y%

    Call PrintPageText(game, "control", CsrLin, String$(game.levelSize + 2, "-")) ' Bottom border

    nextRow% = CsrLin
    Locate curRow% + game.doorY, game.doorX + game.cursor.lastColumn
    Print "*"
    If (game.isOver = 0) And (game.playerX > 0) And (game.playerY > 0) Then
        symbol$ = playerSymbol(game.playerDirection)
        Call WorldScene(RETRIEVE, 1, game.level, game.playerX, game.playerY, scene)
        If scene.color = _RGB(255, 255, 255) Then
            Color _RGB(0, 0, 0) ' For readability, don't print white text on a white background
        Else
            Color _RGB(255, 255, 255)
        End If
        Locate curRow% + game.playerY, game.playerX + game.cursor.lastColumn
        Print symbol$
    End If
    Color _RGB(0, 0, 0)
    Locate nextRow%, 1

    ' Display game control choices
    If Contains%(game.inventory, "GAME BOOK", ",") Then
        Color _RGB(0, 0, 0) ' black
        Locate game.cursor.lastControlRow + 1, 1
        game.controlChoicesRow = CsrLin + 1
        gamePrompt$ = "Game Choices: "
        Call PrintPageText(game, "control", CsrLin, gamePrompt$)
        _PrintMode _FillBackground ' Replace background instead of displaying just text over the image
        Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan on white
        controlChoices$ = game.controlChoices
        Call PrintPageText(game, "control", CsrLin, controlChoices$)
        game.controlChoicesColumn = game.cursor.lastColumn
        _PrintMode _KeepBackground
        Color _RGB(0, 0, 0)
        Call PrintPageText(game, "control", CsrLin, "[Use arrow keys to turn and move]")
    End If
End Sub

' Refresh the story page by evaluating a choice from the player
'
' Parameters:
' game - Book instance
' choice - Player's selected choice
Sub RefreshStoryPage (game As Book, choice As String)
    If (Not Contains%(game.storyChoices, choice, ",")) And (Not Contains%(game.controlChoices, choice, ",")) And (Not Contains%(game.secretChoices, choice, ",")) Then
        Call TurnPage(game, 0)
        If choice <> "" Then
            ' Handle invalid choice
            Call DumbAction(game)
        End If
    Else
        ' Reset the screen
        Call TurnPage(game, 1)
        ' Handle valid choices
        If choice = "EXIT GAME" Then
            Call ExitAction(game)
        End If
        If choice = "LOOK" Then
            Call LookAction(game)
        End If
        If choice = "CREDITS" Then
            Call DisplayCreditsPage(game)
            choice = "LOOK"
            Call TurnPage(game, 1)
            Call LookAction(game)
        End If
        If choice = "HIGH SCORES" Then
            Call DisplayHighScoresPage(game)
            choice = "LOOK"
            Call TurnPage(game, 1)
            Call LookAction(game)
        End If
        If choice = "FLIP BOOK" Then
            If game.isRightHanded = 0 Then
                game.isRightHanded = 1
            Else
                game.isRightHanded = 0
            End If
            Call TurnPage(game, 0)
            Call LookAction(game)
        End If
        If choice = "GO RIGHT-HANDED" Then
            game.isRightHanded = 1
            game.controlChoices = "CREDITS,GO LEFT-HANDED,HIGH SCORES,EXIT GAME"
            Call TurnPage(game, 0)
            Call LookAction(game)
        End If
        If choice = "LISTEN" Then
            Call ListenAction(game)
        End If
        If choice = "MOVE AHEAD" Then
            Call MoveAheadAction(game)
        End If
        If choice = "MOVE BACKWARDS" Then
            Call MoveBackwardsAction(game)
        End If
        If choice = "TURN LEFT" Then
            Call TurnLeftAction(game)
        End If
        If choice = "TURN RIGHT" Then
            Call TurnRightAction(game)
        End If
        If (choice = "CHASE BUNNIES") Or (choice = "SPEAK NAME") Or (choice = "PLAY GAME") Or (choice = "TAME FLUFF") Or (choice = "TAKE GOLD") Or (choice = "NIGHT OWL") Then
            Dim scene As Scene
            Call WorldScene(RETRIEVE, 1, game.level, game.playerX, game.playerY, scene)
            Call WorldScenePage(RETRIEVE, scene.id, 1, pageContents$)
            Color scene.color
            Call DisplayStoryPage(game, scene, pageContents$, choice, 1)
        End If
        If (choice = "ALL THE THINGS") Then
            Call AllTheThingsAction(game)
        End If
    End If
End Sub

' Replay an already open sound file
'
' Parameters:
' soundHandle - LONG-type sound handle returned by PlaySound&
' volume - 0 to 1 value for volume (0=no sound, 1=max)
' loopSound - Play the sound in a continuous loop? (1/0)
Sub ReplaySound (soundHandle As Long, volume As Single, loopSound As Integer)
    If soundHandle& = 0 Then
        Beep
    Else
        _SndVol soundHandle&, volume
        _SndPlay soundHandle&
        If loopSound Then
            _SndLoop soundHandle&
        End If
    End If
End Sub

' Set a special player mode
'
' Parameters:
' game - Book instance
' mode - Special player mode
Sub SetPlayerMode (game As Book, mode As String)
    game.playerMode = mode
    storyColor& = _DefaultColor
    Color _RGB(255, 140, 0)
    Call PrintPageText(game, "story", CsrLin + 1, "PLAYER MODE +" + mode)
    storyRow% = CsrLin
    Call RefreshControlPage(game, "")
    Color storyColor&
    Locate storyRow%, 1
End Sub

' Stop playing a sound file
'
' Parameters:
' soundHandle - LONG-type sound handle returned by PlaySound&
Sub StopSound (soundHandle As Long)
    If soundHandle <> 0 Then
        _SndStop soundHandle
        _SndClose soundHandle
    End If
End Sub

' Prompt the player for a story choice
'
' Parameters:
' game - Book instance
' prompt - Prompt text
' choices - Comma-delimited prompt choices
' enableArrowKeys - Enable use of the keyboard arrow keys? (1/0)
'                   Up - "MOVE AHEAD"
'                   Down - "MOVE BACKWARDS"
'                   Left - "TURN LEFT"
'                   Right - "TURN RIGHT"
' enableMouse - Enable use of the mouse? (1/0)
' inputLength - Max number of characters allowed for user input
'
' Returns:
' User input
Function StoryPrompt$ (game As Book, prompt As String, choices As String, enableArrowKeys As Integer, enableMouse As Integer, inputLength As Integer)
    prompt = prompt + ": "
    game.storyChoices = choices
    startingRow% = CsrLin
    game.storyChoicesRow = startingRow% + 1
    Call PrintPageText(game, "story", startingRow%, prompt)
    _PrintMode _FillBackground ' Replace background instead of displaying just text over the image
    Color _RGB(0, 139, 139), _RGB(255, 255, 255) ' dark cyan on white
    pageChoicesText$ = game.storyChoices ' TODO - This is dumb.  All variables passed to a function are passed by reference.  PrintPageText() will empty out the game.storyChoices if you pass them directly.
    Call PrintPageText(game, "story", CsrLin, pageChoicesText$)
    game.storyChoicesColumn = game.cursor.lastColumn
    _PrintMode _KeepBackground
    Print
    StoryPrompt$ = GetInput$(game, "Enter Choice", enableArrowKeys, enableMouse, inputLength)
End Function

' Story Variable Class
'
' Parameters:
' event - Collection event integer (CREATE, RETRIEVE, UPDATE, DELETE, EXISTS)
' *variable - Variable instance
Sub StoryVariable (event As Integer, variable As Variable)
    Static variableCount As Integer, variables() As Variable

    If event <> RETRIEVE Then
        Dim currentVariable As Variable
        currentVariable.name = variable.name
        Call StoryVariable(RETRIEVE, currentVariable)
    End If

    If event = CREATE Then
        If currentVariable.id > 0 Then
            ' The variable already exists so handle this as an UPDATE event
            variable.id = currentVariable.id
            Call StoryVariable(UPDATE, variable)
        Else
            variableCount = variableCount + 1
            If variableCount = 1 Then
                ' One time only array init
                Dim variables(1 To 100) As Variable
            End If
            variable.id = variableCount
            variables(variable.id) = variable
        End If
    End If
    If event = RETRIEVE Then
        found = 0
        For i = 1 To variableCount
            currentVariable = variables(i)
            If variable.name = currentVariable.name Then
                found = 1
                Exit For
            End If
        Next i
        If found = 1 Then
            variable = currentVariable
        End If
    End If
    If event = UPDATE Then
        If currentVariable.id > 0 Then
            variable.id = currentVariable.id
            variables(variable.id) = variable
        Else
            ' The variable does not already exist so handle this as a CREATE event
            Call StoryVariable(CREATE, variable)
        End If
    End If
    If event = DELETE Then
        ' Unsupported at this time
    End If
End Sub

' Turn Left action
'
' Parameters:
' game - Book instance
Sub TurnLeftAction (game As Book)
    game.playerDirection = game.playerDirection - 1
    If game.playerDirection = 0 Then
        game.playerDirection = 4
    End If
    Call LookAction(game)
End Sub

' Wipe the current screen and redisplay the book image
'
' Parameters:
' game - Book instance
' playPageSound - Play the page turn sound? (1/0)
Sub TurnPage (game As Book, playPageSound As Integer)
    If playPageSound Then
        ' Play a sound for turning the page
        Call ReplaySound(game.pageTurnSound, 1, 0)
    End If

    Cls , _RGB(0, 0, 0) ' Clear screen with black as the background color
    _PutImage , game.image ' Display the main book image
    _PrintMode _KeepBackground ' Preserve the image background when printing text
    Color _RGB(0, 0, 0) ' black

    ' Print the page headers (the book image allows for text starting on row 3)
    If game.chapter <> "" Then
        ' TODO - Need to right-align the text on the right-hand page
        Call PrintPageText(game, "story", (game.firstRow), UCase$(game.chapter))
        Call PrintPageText(game, "control", (game.firstRow), UCase$(game.title))
    End If

    ' All non-header text should begin on row 5 to accommodate the book image
    Locate game.firstRow + 2, 1
End Sub

' Turn Right action
'
' Parameters:
' game - Book instance
Sub TurnRightAction (game As Book)
    game.playerDirection = game.playerDirection + 1
    If game.playerDirection = 5 Then
        game.playerDirection = 1
    End If
    Call LookAction(game)
End Sub

' World Scene Class
'
' Parameters:
' event - Collection event integer (CREATE, RETRIEVE, UPDATE, DELETE, EXISTS)
' world - World number
' Level - Level number
' x - x-coordinate
' y - y-coordinate
' *scene - Scene instance
Sub WorldScene (event As Integer, world As Integer, level As Integer, x As Integer, y As Integer, scene As Scene)
    Static sceneCount As Integer, scenes() As Scene

    If event = CREATE Then
        sceneCount = sceneCount + 1
        If sceneCount = 1 Then
            ' One time only array init
            Dim scenes(1 To 10, 0 To 10, 0 To 10, 0 To 10) As Scene
        End If
        scene.id = sceneCount
        scenes(world, level, x, y) = scene
        ' Initialize the scene's pages
        For pageNumber% = 1 To 10
            Call WorldScenePage(CREATE, scene.id, pageNumber%, "")
        Next pageNumber%
    End If
    If event = RETRIEVE Then
        If (x >= 0) And (y >= 0) Then
            scene = scenes(world, level, x, y)
        Else
            Dim emptyScene As Scene
            scene = emptyScene
        End If
    End If
    If event = UPDATE Then
        scenes(world, level, x, y) = scene
    End If
    If event = DELETE Then
        ' Unsupported at this time
    End If
End Sub

' World Scene Page Class
'
' Parameters:
' sceneId - Scene Id
' pageNumber - Page number
' *contents - Full text and tags of page contents
Sub WorldScenePage (event As Integer, sceneId As Integer, pageNumber As Integer, contents As String)
    Static pageCount As Integer, pages() As String

    If event = CREATE Then
        pageCount = pageCount + 1
        If pageCount = 1 Then
            ' One time only array init
            Dim pages(1 To 1000, 1 To 10) As String
        End If
        pages(sceneId, pageNumber) = contents
    End If
    If event = RETRIEVE Then
        contents = pages(sceneId, pageNumber)
    End If
    If event = UPDATE Then
        pages(sceneId, pageNumber) = contents
    End If
    If event = DELETE Then
        ' Unsupported at this time
    End If
End Sub


